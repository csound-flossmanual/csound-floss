# 07 Hello p-Fields

### What you learn in this tutorial

- What **instrument instances** are.
- How to **open instruments** for input parameters from the score.
- How to work with **flexible input parameters** inside an instrument.
- How to write **comments** in Csound code.

## Instruments are Objects which are Instantiated

We have applied until now a lot of useful capacities to our instrument, as
sliding pitch and volume, and an automated fade-in and fade-out.

But this all happens **inside** the instrument. Whenever we call our _Hello_
&nbsp;instrument, it will play the same pitches in the same volume. All we can decide
currently from outside the instrument is to adjust **when**
&nbsp;the instrument shall **start**, and **how long** it will play. As you know from
[Tutorial 04](01-GS-04.md), these informations are submitted to the instrument
via parameters in the score:

- The first parameter, abbreviated **p1**, ist the number or name of the
  instrument which is called.
- The second parameter, abbreviated **p2**, is the start time of this instrument.
- The third parameter, abbreviated **p3**, is the duration of this instrument.

Let us call the instrument _Hello_ with these score lines:

    i "Hello" 0 2
    i "Hello" 3 3
    i "Hello" 9 1

What we actually do is to call **instances** of a certain instrument. In this
case, we create and call three instances of the "Hello" instrument which follow
each other with some pauses between them:

![Three instrument instances in a sequence](../resources/images/01-GS-07-a.png)

We can create as many instances of an instrument as we like. They can follow in
time, as above, or can overlap, as for these score lines:

    i "Hello" 0 7
    i "Hello" 3 6
    i "Hello" 5 1

![Three instrument instances overlapping each other](../resources/images/01-GS-07-b.png)

## Make instruments flexible

But back to the characteristics of our instrument, as it is for now.
We need it to be more open to the world outside itself. For example, we
want to decide in the call of the instrument instance, which MIDI note to
play at the beginning, and which MIDI note to play at the end of the duration.

To achieve this, we **add more p-fields** to our score line.
We write the first MIDI note number as fourth score parameter **p4**, and the
second MIDI number as fifth score parameter **p5**:

    i "Hello" 0 2 72 68

Which means: We call as

**p1** the instrument "Hello"  
**p2** the start time of this instance to be zero  
**p3** a duration of two seconds  
**p4** the MIDI note number 72 at the beginning, and  
**p5** the MIDI note number 68 at the end of the duration.

To make this happen, we insert **p4** and **p5** in the instrument code.

```csound
instr Hello
  iMidiStart = p4
  iMidiEnd = p5
  kMidi = linseg:k(iMidiStart,p3,iMidiEnd)
  ...
endin
```

The instrument interpretes these values in the same way as it was for **p3**
&nbsp;which we already used in our code. For **p4** and **p5**, the instrument
instance will look at the score line, and take the fourth parameter as value
for **p4**, and the fifth parameter as value for **p5**.

![Instrument reading p4 and p5 of a sore line](../resources/images/01-GS-07-c.png)

## Direct Insertion of p-fields or Variable Definition

We have used **p4** and **p5** in the code above to define the _i-rate_
variables, _iMidiStart_ and _iMidiEnd_. It would have been perfectly fine,
from Csound's side, to set **p4** and **p5** directly in the code, like this:

    kMidi = linseg:k(p4,p3,p5)

I say "from Csound's side" because I think for the readability of code it is
better to tell on top of the instrument code, what **p4** and **p5** mean, and
connect them with a variable name.

This variable will be _i-rate_ variable because the score can only pass
fixed values to the instrument.

The variable name should be as meaningful as possible, without becoming too
long. Most important, again, is the readability of the code.

## Comments in the code

This readability can also be improved by comments; in particular for large code.

Csound offers three possibilities for comments.

- `//` and `;` are the comment signs for one line.
- What is between `/*` and `*/` will also be ignored by Csound. It can
  comprise more than one line.

I suggest that you comment extensively; in particular when you start learning Csound.
It will help you to understand what is happening in the code, and it will
help you to understand your own code later.

I often even start with comments when I code. Then the comments are there to
make clear what I want to do. For instance:

    instr Dontknowyet
      //generate two random numbers

      //calculate their distance

      //if the distance is larger than x ...

      //... insert some silence ...

      //... otherwise make a lot of crazy noise
    endin

## Example

We will now insert comments in the code. At first extensively; in later tutorials
we will reduce it and focus on the new parts of the code.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

/* CONSTANTS IN THE INSTRUMENT HEADER*/
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

/* INSTRUMENT CODE */
instr Hello ;Hello is written here without double quotes!
  //receive MIDI note at start and at end from the score
  iMidiStart = p4 ;this is a MIDI note number
  iMidiEnd = p5
  //create a glissando over the whole duration (p3)
  kMidi = linseg:k(iMidiStart,p3,iMidiEnd)
  //create a decay from -10 dB to -20 dB in half of the duration (p3 / 2)
  kDb = linseg:k(-10,p3/2,-20)
  //sine tone with ampdb and mtof to convert the input to amp and freq
  aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))
  //apply one second of fade out
  aOut = linen:a(aSine,0,p3,1)
  //output to all channels
  outall(aOut)
endin

</CsInstruments>
<CsScore>
/* SCORE LINES*/
//score parameter fields
//p1      p2 p3   p4 p5
i "Hello" 0  2    72 68 ;here we need "Hello" with double quotes!
i "Hello" 4  3    67 73
i "Hello" 9  5    74 66
i "Hello" 11 .5   72 73
i "Hello" 12.5 .5 73 73.5
</CsScore>
</CsoundSynthesizer>
```

## Looking Back to History

"P fields" are an element in which Csound's long history reflects.
This is a figure from the 1962 article "Musical Sounds from Digital Computers"
by Mathews, Pierce and Guttman, showing a "Computer Card":^[
Gravesaner Bl√§tter (Ed. Hermann Scherchen) VI, Heft 23-24 (1962), p. 115,
online: https://soundandscience.de/text/gravesaner-blatter-jahrgang-vi-heft-23-24]

![p-fields on a computer card in Mathews et.al. 1962](../resources/images/01-GS-07-d.png)

Here the _P0_ field contains the information about either "note" or "pause".
_P1_ is the instrument number. As there is no polyphony here, _P2_ is the duration
of a chain of events. _P4_ is an amplitude here in the range of 0 through 1000,
and _P5_ is the frequency.

So p-fields were there before computers had a screen and a keyboard ...

It was Mathews' _MUSIC V_ which Jean-Claude Risset used to write his epochal
"Catalogue of Computer Synthesized Sounds" in 1969. His code can with only
slight modifications be transformed into Csound code. Here is a snippet in which
you can see p-fields again:^[Jean-Claude Risset, An Introductory Catalogue of
Computer Synthesized Sounds, Bell Telephone Laboratories, Murray Hill, New
Jersey, 1969, p. 56; online:
https://ia801707.us.archive.org/13/items/an-introductory-catalogue-of-computer-synthesized-sounds/An-Introductory-Catalogue-of-Computer-Synthesized-Sounds.pdf]

![Excerpt from Rissets "Catalogue" 1969](../resources/images/01-GS-07-e.png)

The early versions of Max Mathews _MUSIC_ program could only run on one particular
computer. The "C" in Csound points to the
[C Programming Language](<https://en.wikipedia.org/wiki/C_(programming_language)>)
which was first released in 1972. It made it possible to seperate the source code
which is written and can be read by humans from a specific
machine on which it runs. C is still a very successful language, used
for everything which must be fast, like operating systems or audio applications.

_P fields_ are on one hand simple and give a lot of possibilities. There are, on the
other hand, restrictions. Basically, a _p-field_ carries a number. It took a
lot of work from the Csound developers to make it possible to write a string in a
p-field, too. But it is still not possible to pass a signal via a _p-field_
&nbsp;to an instrument.

Fortunately, _p-fields_ are only one possibility for an instrument to communicate
with the "outer world". We will discuss other ways later in these Tutorials.

## Try it yourself

- Change the values in the score in a way that all directions of the pitch slides
  are reversed (upwards instead of downwards and vice versa).
- Change the values in the score so that you have not any more a sliding pitch
  but a constant one.
- Add two p-fields in the score to specify the first and the last volume as _dB_.
  Refer to these p-fields as **p6** and **p7** in the instrument code.
  Introduce two new variables and call them _iDbStart_ and _iDbEnd_.
- Change the code so that the volume change uses the whole duration of the
  instrument instance whilst the pitch change only uses half of the instrument's
  duration.
- Go back to the first code by reloading the page. Now remove the fifth p-field
  from the score and change the code in the instrument so that
  the _iMidiEnd_ variable is always 6 MIDI notes lower than _iMidiStart_.
- Introduce **p5** again, but now with a different meaning: 1 in this p-field
  means that the _iMidiEnd_ note will be six MIDI keys higher than _iMidiStart_;
  -1 will mean that _iMidiEnd_ will be six MIDI keys lower than _iMidiStart_.
- Add a p-field which establishes the fade-out duration as ratio to the whole
  duration. (1 would mean: fade out equals the overall instrument duration; 0.5
  would mean: fade out time is half of the instrument duration.)

## Terms and symbols you have learned in this tutorial

### Terms

- _instance_ is the manifestation or realization of an instrument
  when it is called by a score line

### Symbols

- **p4**, **p5** ... are used in a Csound instrument code to refer to the fourth,
  fifth ... parameter in the score line which called the instrument instance

## Go on now ...

with the next tutorial: [08 Hello schedule](01-GS-08.md).

## ... or read some more explanations here

### MIDI notes and microtonal deviations

You may have noticed that in the last score line of the example this was written:

    i "Hello" 12.5 .5 73 73.5

But what is MIDI note number 73.5? There is obviously no key with this number.
There is only key number 73 (C#5) and 74 (D5).

This is true but the conversion from MIDI note numbers to frequencies does not
only work for integer MIDI note numbers. It is possible to specify any fraction
of the semitone which is between two integer MIDI note numbers.
We can split a semitone in two quartertones. This is what we did
by referring to MIDI note number 73.5: A quarter tone higher than C#5, or a
quarter tone lower than D5.

In the same way we can express any other fraction. Most common is to divide
one semitone in one hundred cents. So MIDI not number 60.14 would be C4 plus 14 Cent.
Or 68.67 would be A4 minus 33 Cents.

### Enumerate instrument instances as fractional part

It is not only possible to call as many instances of an instrument as we like,
we can also give each of these instances a certain number. This is done by
calling the instrument not as an integer number, but as this integer number
plus a fractional part.

In his case, we will not write these score lines ...

    i 1 0 3
    i 1 2 2
    i 1 5 1

... but we will write this score line instead:

    i 1.1 0 3
    i 1.2 2 2
    i 1.3 5 1

In this case, we have called instrument 1.1 as first instance of instrument 1,
and 1.2 as its second instance, and 1.3 as its third instance.

The instrument instance gets this information, as we see in this simple
example:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

instr 1
  print(p1)
endin

</CsInstruments>
<CsScore>
i 1.1 0 3
i 1.2 2 2
i 1.3 5 1
</CsScore>
</CsoundSynthesizer>
```

You should see this console output:

    instr 1:  p1 = 1.100
    instr 1:  p1 = 1.200
    instr 1:  p1 = 1.300

So the instance which we called as 1.1 "knows" that it is instance 1.1, and so
do the other instances. Imagine we want to send a specific message which is
broadcasted on a certain software channel called "radio" only to instance 1.2,
then we would write something like: "If my instance is 1.2 then I will receive
the message from the radio channel."

This is the Csound code for it, without explaining about the opcodes you do
not know yet. But perhaps you can follow the logic, and please have a look
in the console printout.

```csound
<CsoundSynthesizer>
<CsOptions>
-odac -m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

//put the message string "Hello ..." in the channel "radio"
chnset("Hello instrument 1.2!","radio")

instr 1
  iMyInstance = p1 ;get instance as 1.1, 1.2 or 1.3
  print(iMyInstance) ;print it
  //receive the message only if instance is 1.2
  if iMyInstance == 1.2 then
    Smessage = chnget:S("radio")
    prints("%s\n",Smessage)
  endif
endin

</CsInstruments>
<CsScore>
i 1.1 0 3
i 1.2 2 2
i 1.3 5 1
</CsScore>
</CsoundSynthesizer>
```

You should see this in the conlole printout:

    instr 1:  iMyInstance = 1.100
    instr 1:  iMyInstance = 1.200
    Hello instrument 1.2!
    instr 1:  iMyInstance = 1.300

### Instrument names and fractional parts

We can give a name or a number to an instrument. I personally prefer names
because the names describes what the instrument does. For instance

    instr LiveInput
    instr RecordToDisk
    instr AnalyzeRMS

This again improves the readability of the code.

But we cannot call an instrument with a fractional number if we call the
instrument by its name. In the score line ...

    i "LiveInput" 0 1000

... the first _p-field_ `"LiveInput"` is a string, not a number. And we cannot
extend a string with `.1` as we can for the numbers. This will not work:

    i "LiveInput".1 0 1000

We have two options here. The first option uses the fact that Csound internally
converts each instrument name to a number. The way Csound assigns numbers to the
named instruments is simple: The instrument which is on top gets number 1, the
next one gets number 2, and so on.

When we only have one instrument, we can be sure that this is instrument 1 for
Csound. And therefore this code works without problems:

```csound
<CsoundSynthesizer>
<CsOptions>
-odac -m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

//put the message string "Hello ..." in the channel "radio"
chnset("Hello instrument 1.2!","radio")

instr TestInstanceSelection
  iMyInstance = p1 ;get instance as 1.1, 1.2 or 1.3
  print(iMyInstance) ;print it
  //receive the message only if instance is 1.2
  if iMyInstance == 1.2 then
    Smessage = chnget:S("radio")
    prints("%s\n",Smessage)
  endif
endin

</CsInstruments>
<CsScore>
i 1.1 0 3
i 1.2 2 2
i 1.3 5 1
</CsScore>
</CsoundSynthesizer>
```

We have an instrument with the name "TestInstanceSelection" here, but we call
it in the score lines as 1.1, 1.2 and 1.3. No problem.

The other way to work with named instruments and fractional parts is to move
the instrument call from the score to the actual Csound code. This is the
subject of the next Tutorial in which we will introduce the `schedule` opcode.
