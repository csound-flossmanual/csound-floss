# 09 Hello If


### What you learn in this tutorial

- How you can work with **if-then** in Csound.
- How you can **print formatted strings**.


## One Instance Calls the Next ...

In the last Tutorial we have introduced the `schedule` opcode which calls
an instrument instance.

We have put the `schedule` code outside the instrument code. In this case,
it works like a score line.

But what will happen if we put a `schedule` statement also inside an instrument?

Try this:

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr InfiniteCalls
  //play a simple tone
  aSine = poscil:a(.2,415)
  aOut = linen:a(aSine,0,p3,p3)
  outall(aOut)
  //call the next instance after 3 seconds
  schedule("InfiniteCalls",3,2)
endin
schedule("InfiniteCalls",0,2)

</CsInstruments>
<CsScore>
</CsScore>
</CsoundSynthesizer>
~~~

An infinite chain of calls is triggered.

The first call must be outside the instrument:

    schedule("InfiniteCalls",0,2)

This line of code calls the first instance of instrument "InfiniteCalls".

But inside this instrument, again we have an instrument call. Three seconds
after the instrument instance is created, the next instance will be there:

    schedule("InfiniteCalls",3,2)

Note that the start time of the new instance is very important here. If you 
set it to 2 seconds instead of 3 seconds, it will create the next instance 
immediately after the current one. If you set the start time of the next
instance to 1 second, two instances will overlap.


## Schedule Depending on Conditions

This self-scheduling is a very interesting feature in Csound, but usually
we do not want it to be infinite. Instead, we want to make it depending on 
a certain condition.

We will implement now an instrument which triggers itself again six times.
This is what we must do:

- We must pass the number 6 as count variable to the first instrument instance.
- The second instance is then called with number 5 as count variable, and so on.
- If the instance with count variable 1 is there, no more instances are called.

We can draw this program flow:

![Program flow for conditional re-triggering of instrument instances](../resources/images/01-GS-09-a.png)


## The 'if' Opcode in Csound

We can implement this limited chain of self-scheduling instances with the help
of the `if` opcode. Try out this example by running and changing the *iCount*
variable from 6 to 1, and look at the output.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr TryMyIf
  iCount = 6
  if (iCount > 1) then
	prints("True!\n")
  else
    prints("False!\n")
  endif
endin

</CsInstruments>
<CsScore>
i "TryMyIf" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

The keywords here are `if`, `else` and `endif`.
**endif** ends the *if* clause in a similar way as **endin** ends an instrument.

You again see the opcode `prints` here which shows a string in the console. 
We will explain more in the optional part of this tutorial about formatting.

If you want to read more about conditional branching with `if` have a look at
[this](/csound-language/control-structures#if---then---elseif---then---else)
section in [Chapter 03](/csound-language/initialization-and-performance-pass) 
of this book.


## Example

Please run the example and read the code.  
Can you figure out in which way the *iCount* variable is modified?  
Which other parameters are changed from instance to instance?

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  iMidiStart = p4
  iMidiEnd = p5
  kDb = linseg:k(-10,p3/2,-20)
  kMidi = linseg:k(iMidiStart,p3/3,iMidiEnd)
  aSine = poscil:a(ampdb(kDb),mtof(kMidi))
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
  
  iCount = p6
  print(iCount)
  if (iCount > 1) then
	schedule("Hello",p3,p3+1,iMidiStart-1,iMidiEnd+2,iCount-1)
  endif
endin
schedule("Hello", 0, 2, 72, 68, 6)

</CsInstruments>
<CsScore>
</CsScore>
</CsoundSynthesizer>
~~~

## Try it yourself

Change the code so that from instance to instance

- the distance in time between two instances increases
- the distance in time between two instances decreases in the ratio 1/2  
(so that instance 3 has only half the distance to instance 2 as instance
2 had to instance 1)
- the duration increases in the ratio 3/2
- the first MIDI pitch increases by two semitones whilst
- the second MIDI pitch decreases by one semitone


## More About 'if' I: If-Else and If-Elseif-Else

`if` is probably the most important word in any programming language. 
In using `if` we can create branches, and branches of branches, in our program flow. 
As the usage of `if` is so common for us all in daily life,
we can easily transfer it to a programming language, in this case
to Csound.

Some example cases following here, based on daily situations.

### If - else

"If the sun shines then I will go out, else I will stay at home."

This is the Csound version. Please change the *iSun* variable.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr If_Then
  iSun = 1   // 1 = yes, 0 = no
  if (iSun == 1) then
	prints("I go out!\n")
  else
    prints("I stay at home!\n")
  endif
endin

</CsInstruments>
<CsScore>
i "If_Then" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

In `if (iSun == 1)` we ask for equality.  
This is the reason that we use `==` rather than `=`.  
The double `==` **ask** for equality between left and right.  
The simple `=` **sets** the left hand side variable to the right hand side value,
like in `iSun = 1`.  
Csound also accepts `if (iSun = 1)` but I believe it is better to distinguish
these two cases.

The parentheses in `(iSun == 1)` can be omitted but I prefer to put them for
better readability.

Csound does not have an own symbol or keyword for the Boolean "True" and "False".
Usually we use **1** for True/Yes and **0** for False/No.


### If - elseif - else

When we add one or more `elseif` questions, we come to a decision between 
several cases.

"If the pitch is higher than MIDI note 80, then instrument *High* will start.  
Elseif the pitch is higher than MIDI note 60, then instrument *Middle* will start.  
Else instrument *Low* will start."

This is the Csound version. Please change the *iPitch* variable.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr If_Elseif_Then
  iPitch = 90   // change to 70 and 50
  if (iPitch > 80) then
	schedule("High",0,0)
  elseif (iPitch > 60) then
    schedule("Middle",0,0)
  else
    schedule("Low",0,0)
  endif
endin

instr High
  prints("Instrument 'High'!\n")
endin

instr Middle
  prints("Instrument 'Middle'!\n")
endin

instr Low
  prints("Instrument 'Low'!\n")
endin

</CsInstruments>
<CsScore>
i "If_Elseif_Then" 0 1
</CsScore>
</CsoundSynthesizer>
~~~

Note that in the second condition `elseif (iPitch > 60)` we grab all pitches
which are larger than 60 and lower or equal 80, because we only come to this
branch if the first condition `if (iPitch > 80)` is not true.


## More About If II: Nested 'if's; AND and OR

### Nested 'if's

We can have multiple levels of branching, for instance:

"If the sun shines, then:

- If I need some fruits, then I will go to the market,  
- Else I will go in the woods;

Else (= if the sun is not shining):  

- If I am hungry then I will cook some food,  
- Else (= if I am not hungry):  
  - If I am not tired then I will learn more Csound,
  - Else I will have a rest."


~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Nested_If
  // input situation (1 = yes)
  iSun = 1   
  iNeedFruits = 1
  iAmHungry = 1
  iAmTired = 1
  // nested IFs
  if (iSun == 1) then        //sun is shining:
    if (iNeedFruits == 1) then  //i need fruits
      prints("I will go to the market\n")
    else                        //i do not need fruits
      prints("I will go to the woods\n")
    endif                       //end of the 'fruits' clause
  else                      //sun is not shining:
    if (iAmHungry == 1) then    //i am hungry
      prints("I will cook some food\n")
    else                        //i am not hungry
      if (iAmTired == 0) then       //i am not tired
        prints("I will learn more Csound\n")
      else                          //i am tired
        prints("I will have a rest\n")
      endif                         //end of the 'tired' clause
    endif                       //end of the 'hungry' clause
  endif                     //end of the 'sun' clause
endin

</CsInstruments>
<CsScore>
i "Nested_If" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

### Logical AND and OR

Instead of two nested *if* which ought to be true, we can also ask whether
both are true: "If the sun is shining and I have finished my work, I will go out."

In Csound, like in most programming languages, the symbol for this logical AND
is `&&`.

The symbol for the logical OR is `||`.

Here comes and example for both. Try changing the values and watch the output.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr AndOr
  iSunIsShining = 1
  iFinishedWork = 1
  prints("iSunIsShining = %d\n",iSunIsShining)
  prints("iFinishedWork = %d\n",iFinishedWork)
  //AND
  if (iSunIsShining == 1) && (iFinishedWork == 1) then
    prints("AND = True\n")
  else
    prints("AND = False\n")
  endif
  //OR
  if (iSunIsShining == 1) || (iFinishedWork == 1) then
    prints("OR = True\n")
  else
    prints("OR = False\n")
  endif
endin

</CsInstruments>
<CsScore>
i "AndOr" 0 0
</CsScore>
</CsoundSynthesizer>
~~~





## Opcodes you have learned in this tutorial

- `if ... then ... [elseif] ... [else] ... endif` conditional branching 


## Go on now ...

with the next tutorial: [10 Hello Random](01-GS-10.md).


## ... or read some more explanations here




### A short form 

We have a short form for `if`in Csound which is quite handy when we set a 
variable to a certain value depending on a condition.

Instead of ...

    if (iCondition == 1) then
      iVariable = 10
    else
      iVariable = 20
    endif

... we can write:

    iVariable = (iCondition == 1) ? 10 : 20

You can find another example 
[here](/csound-language/control-structures#short-form-a-v-b-x-y) in this book.



### Format strings

The `prints` opcode prints a string to console.

The string can be a *format string*. This means that it has empty parts or place
holders which can be filled by variables.

These place holders always start with `%` and are followed by a character which
signifies a data type. The most common ones are:

- `%d` for an integer
- `%f` for a floating point number
- `%s` for a string

Note that a new line must be written by `\n`. Otherwise the printout is not
put on a new line but follows each other.

Here comes a simple example:

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr FormatString
  prints("This is %d - an integer.\n",4)
  prints("This is %f - a float.\n",sqrt(2))
  prints("This is a %s.\n","string")
  prints("This is a %s","concate")
  prints("nated ")
  prints("string.\n")
endin

</CsInstruments>
<CsScore>
i "FormatString" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

More can be found [here](/how-to/print) in this book.  
The format specifier are basically the same as in the C programming language.
You can find a reference [here](https://cplusplus.com/reference/cstdio/printf/).


### One more item here ...
