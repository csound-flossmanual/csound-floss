# 10 Hello Random

## What you learn in this tutorial

- How to work with random numbers in Csound
- How to set a certain seed for random sequences


## Random numbers and artstic decisions

Throwing the dies and inventing games which are based on the result has always
been enjoyable for people. Most interesting is the relationship between the
decided rules and the inpredictablity of the next dies.

In modern art and music random choices often have an important role. It can
be on a more technical level, for instance if we use random deviations for
[granular synthesis](/sound-synthesis/granular-synthesis#asynchronous-granular-synthesis) 
to somehow imitate nature.

But it can be also an essential part of our invention that we create *structures*
which can be filled in one or the other way, rather than determining each single
note like in a melody. 

We will create a simple example for this way of composing here. It will show that
"working with random" does not at all mean "withdraw from decisions". In
contrary, the decisions are there, and most important for what can happen,
and which results can come out.


## The 'random' opcode and the 'seed'

For getting a random number, we set one limit for the smallest possible number,
and one limit for the biggest possible number. Inside this range, a random
number is selected.

This is a simple example for a random number between 10 and 20. Please run it 
three times and watch the console printout.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m 128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Random
  iRandomNumber = random:i(10,20)
  prints("Random number between 10 and 20: %f\n",iRandomNumber)
endin

</CsInstruments>
<CsScore>
i "Random" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

You will see that three times the same random number is generated.
My printout shows:

    Random number between 10 and 20: 18.828730

Why?

Strictly spoken the computer has no random because it can only calculate.
A random number is created internally by a calculation. Once the first number
is there, all other numbers are determined.

This starting point of a random sequence is called *seed*. If we do not set
a seed, Csound uses a fixed number. This is the reason why we always got the
same number.

The `seed` opcode offers us two possibilities:

- For `seed(0)` Csound will seed from the current clock. This is what most other
applications do as default. It results in an always changing start value, so
that is what we usually want to get when we use random.
- For any positive integer number we put in `seed`, for instance `seed(1)` or
`seed(65537)`, we get a certain start value of the random sequence. `seed(1)`
will yield another result as `seed(65537)`. But once you run your Csound program
twice with `seed(1)`, it will result in the same random values.  
This is a good opportunity to check out different random traces, but being able
to reproduce one which you like or need to check.

Please insert `seed(0)` in the example above. It should be placed below the
`0dbfs = 0` line, in the global space of the orchestra. When you run your 
code several times, it should always print a different *iRandomNumber* output.

Also try to insert `seed(1)` or `seed(2)` etc. instead. You will see that each
is different, but once you run one of them twice, you get the same result.



## Example

Please this time read the code first, and make a guess how it will sound for
each note. And what will be the development of this sketch, and how long do
you expect it to go?


~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1
seed(12345)

instr Hello
  //MIDI notes between 55 and 80 for both start and end
  iMidiStart = random:i(55,80)
  iMidiEnd = random:i(55,80)
  //decibel between -30 and -10 for both start and end
  iDbStart = random:i(-30,-10)
  iDbEnd = random:i(-30,-10)
  //calculate lines depending on the random choice
  kDb = linseg:k(iDbStart,p3/2,iDbEnd)
  kMidi = linseg:k(iMidiStart,p3/3,iMidiEnd)
  //create tone with fade-out and output
  aSine = poscil:a(ampdb(kDb),mtof(kMidi))
  aOut = linen:a(aSine,0,p3,p3/2)
  outall(aOut)
  
  //trigger next instance with random range for start and duration
  iCount = p4
  if (iCount > 0) then
    iStart = random:i(1,3)
    iDur = p3 + random:i(-p3/2,p3)
    schedule("Hello",iStart,iDur,iCount-1)
  endif
endin
schedule("Hello", 0, 2, 15)

</CsInstruments>
<CsScore>
</CsScore>
</CsoundSynthesizer>
~~~

## Structural decisions

We have a lot of `random` opcodes in the code. Let us look closer to the 
decisions behind them, and to the effect which results from the decisions.

    iMidiStart = random:i(55,80)
    iMidiEnd = random:i(55,80)

Setting both, start and end pitch of the *glissando* line to a range from 
MIDI note 55 (= *F#4*) to 80 (= *G#6*) makes it equally probable that rising
and falling lines will appear. Some will have a big range (imagine a line
from 78 to 56), other will have a small range (imagine a line from 62 to 64).

The alternative could be, for instance, to set:

    iMidiStart = random:i(55,67)
    iMidiEnd = random:i(68,80)

Then the sliding pitch line would always be upwards. Or:

    iMidiStart = random:i(55,70)
    iMidiEnd = random:i(65,80)

Then the pitch line would be mostly upwards, but sometimes not.

Similar decisions apply for the volume line which is set to:

    iDbStart = random:i(-30,-10)
    iDbEnd = random:i(-30,-10)

The maximum difference is 20 dB which is not too much. So there is some 
variance between louder and softer tones but all are well perceivable, and
there is not much foreground-background effect as it would probably occur
with a range of say -50 to -10 dB.

The most important decisions for the form are these, concerning the distance
between subsequent notes and the duration of the notes:

    iStart = random:i(1,3)
    iDur = p3 + random:i(-p3/2,p3)

The distance between two notes is between one and three seconds. So in average
we have a new note every two seconds.

But the duration of the notes is managed in a way that the next note duration
has this note's duration (**p3**) plus a random range between  
- minus half of this note's duration as minimum, and
- this note's duration as maximum.

For the first note which has a duration of two seconds, this means a random
range between one and four seconds. So the tendency of the duration is to
become larger and larger. Here is what happens in the example code above
for the first seven notes:

![Duration development for notes 1-7](../resources//images/01-GS-10-a.png)

It is interesting to see that note 2 and 3 expand their duration as expected,
but then note 4 and 5 shrink because they chose their durations close to the
minimum. Later more and more notes sound at the same time, forming chords
or clusters.


## Try it yourself

- Set `seed(0)` instead of `seed(12345)` and listen to some of the results.
- Change line 32 `iDur = ...` so that you get equal probability for longer
or shorter durations, without any process.  
Make up your mind about this version.
- Change line 32 `iDur = ...` so that on the long run the durations become
shorter and shorter.
- Change the code so that for half of the notes to be played the durations
become longer, and for the second half the durations become shorter.
- Change the code so that for the first half of the notes the distance 
between subsequent notes become shorter, and in the second half again
become longer.
- Apply this change also to the pitches and the volume so that in the first
half the pitches increase whilst the volume descreses, and then in the second
half vice versa.



## Opcodes you have learned in this tutorial

- `random:i(iMin,iMax)`
- `seed iNum` 



## Go on now ...

with the next tutorial: [11 Hello Keys](01-GS-11.md).


## ... or read some more explanations here

### Other random opcodes


### Random is not random
