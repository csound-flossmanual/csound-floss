# 05 Hello MIDI Keys


### What you learn in this tutorial

- How to work with **MIDI note numbers** instead of raw frequency values.
- How to **convert** MIDI notes **to frequencies**
- What **i-rate variables** in Csound are.
- How to **print i-rate variables** in the Csound console.



## Problems of Using Raw Frequencies

In [Hello Frequency](01-GS-02.md) we created a line, or in musical terms,
a *glissando* from 500 Hz to 400 Hz:

    kFreq = linseg:k(500,0.5,400)

Actually we have two problems here:

1. When we ask a musician to play a certain pitch, we say "Can you please play
D", or "Can you please play D4".  
But what pitch is 500 Hz?

2. When we perform a glissando from 500 Hz to 400 Hz in the way we did, we do
it actually not in the linearity we thought we would.

We will discuss the first issue in the next paragraphs.  
For the second issue, you find an explanation below, in the optional part
of this tutorial. You may also have a look at the
[Pitches](/basics/pitch-and-frequency#pitches) section in the 
[Ditigal Audio Basics](/basics/digital-audio) chapter of this book.


## MIDI Note Numbers and the 'mtof' Opcode

There are several systems and possibilities to specify pitches like *D4* instead
of raw frequencies in Csound.

I recommend using MIDI note numbers, because they are easy to learn 
and they are used also by other applications and programming languages.

All you must know about MIDI keys or note numbers: *C4* is set to note number
60. And then each semitone, or each next key on a MIDI keyboard, 
is *plus one* for upwards, and *minus one* for downwards.

OVERVIEW

If you want to transform any MIDI key to its related frequency, use the
`mtof` (midi to frequency) opcode.

When we want Csound to calculate the frequency which is related to *D4*,
and store the result in a variable, we write:

    iFreq = mtof:i(62)


## i-rate Variables in Csound 

The MIDI key 62 is a number, not a signal. 
And so is the frequency which relates to this MIDI key.
It is a single number, not a signal.
This is the reason that we called this variable ***i**Freq*.

An **i-rate** variable in Csound is only calculated **once**: when the instrument
in which it occurs is initialized.  That is where the name comes from.

Remember that **k-rate** and **a-rate** variables are **signals**. A signal varies in 
time. This means that its values are re-calculated all the time. To summarize:

- **a-rate** variables are signals which are updated every sample.
- **k-rate** variables are signals which are updated every k-cycle. This is 
less often than the *a-rate*, and determined by the `ksmps` constant.
- **i-rate** variables are no signals but numbers which are only calculated 
once for each instrument call. They keep this value during the performance of 
the instrument's note and will not change it.



## The 'print' Opcode

Now we would like to know which frequency the *iFreq* variable holds.

For a programming language this means to *print*. "Printing" means: To show values
in the console. In the console we see the messages from the program.

The `print` opcode is what we are looking for. Its syntax is simple:

    print(iVariable)
    
Please run the following code, and look in the console.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Print
  iFreq = mtof:i(62)
  print(iFreq)
endin

</CsInstruments>
<CsScore>
i "Print" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

You should see this message near the end of the console output:

    instr 1:  iFreq = 293.665

So we know that the frequency which is related to the MIDI key number 62,
or *D4*, is 293.665 Hz.

Note: The `print` opcode is **for i-rate variables only**. You can **not**
use this opcode for printing *k-rate* or *a-rate* variables.  
We will show opcodes for printing control- or audio-variables later.


## Example

We will use now MIDI notes for the glissando, instead of raw frequencies.

We create a line which moves in half a second from MIDI note 72 (C5) to 
MIDI note 68 (Ab4). This line we store in the Variable *kMidi*:

    kMidi = linseg:k(72,0.5,68)

This line is a signal because it changes in time.

Afterwards we convert this line to frequencies:

    kFreq = mtof:k(kMidi)

Note that we use `mtof:k` here because we apply the midi-to-frequency
converter to the *k-rate* signal *kMidi*. The result is a *k-rate* signal, too.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kAmp = linseg:k(0.3,0.5,0.1)
  kMidi = linseg:k(72,0.5,68)
  kFreq = mtof:k(kMidi)
  aSine = poscil:a(kAmp,kFreq)
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~ 

## The 'prints' Opcode and Strings

So far we have only dealt with *numbers*.

We have discussed that numbers can only be calculated once, at *i-rate*, 
or that they are calculated again and again for a block of samples, so
being *k-rate*, or even are calculated again and again for each sample,
so being *a-rate*.

But all are numbers.

This is somehow natural for an audio application, compared to a text program.

But even in an audio application we need something to write text in, for instance
when we point to a sound file as "myfile.wav".

This data type, which starts and ends with double quotes, is called a **string**.

The `prints` opcode is similar to the `print` opcode, but it prints a string,
and not a number. Try this:

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Prints
  prints("Hello String!\n")
endin

</CsInstruments>
<CsScore>
i "Prints" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

Perhaps you wonder about the `\n` at the end of the string.

This is a format specifier. It adds a new line. 

Please compare: Remove the two characters `\n` and run the code again. You will
see that the console printout is now immediately followed by the next Csound
message.

In fact, there are much mor format specifiers. We will get back to them
and `prints` in [Tutorial 09](/get-started/GS-09#format-strings).


## Try it yourself

Change the *kMidi* signal so that

1. the first MIDI key is *E5* rather than *C5*
2. the second MIDI key is *G4* rather than *Ab4*
3. the whole duration of the instrument is used for the *glissando*
4. the glissando goes upwards rather than downwards.

Change also this:

5. Create two variables *iFreqStart* and *iFreqEnd* for the the two MIDI notes.
(You need to convert the MIDI notes at *i-rate* for it.)  
Insert then these *i-variables* in the `kFreq = linseg(...)` line.  
Compare the result to the one in the example.
6. Code a "chromatic scale" (= using always the next MIDI note) which falls from
*D5* to *A4*. Each MIDI key stays for one second, and then immediately moves 
to the next step. You can get this with `linseg` by using zero as duration 
between two notes. This is the start:  
`kMidi = linseg:k(74,1,74,0,73,...)`  
Don't forget to adjust the overall duration in the score; otherwise you will not
hear the series of pitches although you created it ...


## Opcodes and terms you have learned in this tutorial

### Opcodes

- `mtof:i(MIDI_note)` MIDI-to-frequency converter for one MIDI note
- `mtof:k(kMIDI_notes)` MIDI-to-frequency converter for a *k-rate* signal
- `print(iVariable)` prints *i-rate* variables to the Csound console
- `prints(String)` prints a string to the Csound console

### Terms

- *i-rate* is the "time" (as point, not as duration) in which an instrument is
initialized
- *i-rate variable* or *i-variable* is a variable which gets a value only
once, at the initialization of an instrument.


## Go on now ...

with the next tutorial: [06 Hello Decibel](01-GS-06.md).


## ... or read some more explanations here

### The same is not the same ...

It is worth to look a bit closer to the question of pitch line versus
frequency line. We have two possibilities when we create a *glissando* between
two MIDI notes:

1. We first create the line between the two MIDI notes. Afterwards we convert
this line to the frequencies. This is what we did in the example code:

~~~csound
kMidi = linseg:k(72,0.5,68)
kFreq = mtof:k(kMidi)
~~~

2. We first convert the two MIDI notes to frequencies. Afterwards we create
a line. This would be the code:

~~~csound
iFreqStart = mtof:i(72)
iFreqEnd = mtof:i(68)
kFreq = linseg:k(iFreqStart,0.5,iFreqEnd)
~~~

For better comparision, we change the code so that  
- we move during 12 seconds instead of 0.5 seconds  
- we move two octaves instead of four semitones.

We choose *A5* (= 880 Hz or MIDI note 81) and *A3* (= 220 Hz or MIDI note 57) 
as start and end. And we create one variable for each way.

    kMidi = linseg:k(81,12,57)
    kFreqLine_1 = mtof:k(kMidi)
    
    iFreqStart = mtof:i(81)
    iFreqEnd = mtof:i(57)
    kFreqLine_2 = linseg:k(iFreqStart,12,iFreqEnd)

When we use one oscillator for each frequency line, we can listen to both
versions at the same time. We will output the *kFreqLine_1* in the left channel,
and the *kFreqLine_2* in the right channel.

The MIDI pitches of the *kFreqLine_1* are contained in the *kMidi* variable.
But which MIDI pitches comply with the frequencies of the *kFreqLine_2*? 
We can get these pitches via the `ftom` (frequency to midi) opcode.

This is the code which plays both lines, and prints out MIDI and frequency values
of both lines, once a second. Don't worry about the opcodes which you do not know
yet. It is mostly about "pretty printing".

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 1
nchnls = 2
0dbfs = 1

instr Compare

  kMidi = linseg:k(81,12,57)
  kFreqLine_1 = mtof:k(kMidi)
  
  iFreqStart = mtof:i(81)
  iFreqEnd = mtof:i(57)
  kFreqLine_2 = linseg:k(iFreqStart,12,iFreqEnd)
  kMidiLine_2 = ftom:k(kFreqLine_2)
  
  prints("Time   Pitches_1  Freqs_1      Freqs_2  Pitches_2\n")
  prints("(sec)   (MIDI)     (Hz)          (Hz)     (MIDI)\n")
  printks("%2d      %.2f     %.3f      %.3f    %.2f\n", 1,
          timeinsts(), kMidi, kFreqLine_1, kFreqLine_2, kMidiLine_2)
  
  aOut_1 = poscil:a(0.2,kFreqLine_1)
  aOut_2 = poscil:a(0.2,kFreqLine_2)
  aFadeOut = linen:a(1,0,p3,1)
  out(aOut_1*aFadeOut,aOut_2*aFadeOut)
  
endin

</CsInstruments>
<CsScore>
i "Compare" 0 13
</CsScore>
</CsoundSynthesizer>
~~~ 

In the console, you should see this:

~~~
Time   Pitches_1  Freqs_1      Freqs_2  Pitches_2
(sec)   (MIDI)     (Hz)          (Hz)     (MIDI)
 0      81.00     880.000      880.000    81.00
 1      79.00     783.991      825.000    79.88
 2      77.00     698.456      770.000    78.69
 3      75.00     622.254      715.000    77.41
 4      73.00     554.365      660.000    76.02
 5      71.00     493.883      605.000    74.51
 6      69.00     440.000      550.000    72.86
 7      67.00     391.995      495.000    71.04
 8      65.00     349.228      440.000    69.00
 9      63.00     311.127      385.000    66.69
10      61.00     277.183      330.000    64.02
11      59.00     246.942      275.000    60.86
12      57.00     220.000      220.000    57.00
~~~

When we plot the two frequency lines, we see that the first one looks like a
concave curve, whilst the second one is a straight line:

FREQ 1 VS FREQ 2

We see that *Freqs_1* reaches 440 Hz after half of the duration, whilst *Freqs_2*
reaches 550 Hz after half of the duration. 

The *Freqs_2* line *subtracts* 55 Hz in each second of performance. 

    880.000 - 55.000 = 825.000
    825.000 - 55.000 = 770.000
    ...
    275.000 - 55.000 = 220.000

The *Freqs_1* line on the other hand has the same *ratio* between the 
frequency values of two subsequent seconds:

    880.000 / 783.991 = 1.12246...
    783.991 / 698.456 = 1.12246...
    ...
    246.942 / 220.000 = 1.12246...

Our perception follows ratios. What we hear is what the *Pitches* printout 
shows:

PITCHES 1 VS PITCHES 2

We hear that the first line is constantly falling, whilst the second one is
too slow at the beginning and too fast at the end.

To be written in muscial notation, this is hoe this "too slow" and "too fast"
look like:

NOTEN von PITCH 2

If you enjoy this way of glissando, no problem. Just be conscious that it is 
not as linear as its frequencies suggest.


### Converting Frequency to MIDI: 'ftom'

Usually we want to convert MIDI note numbers to frequencies. Because we want to
and should work in MIDI note numbers, but most opcodes need a frequency as
input.

But sometimes it is good to know which MIDI note number corresponds with a 
given frequency. Thanks to the `ftom` opcode we can ask Csound for it.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr FTOM //convert frequency to midi
  iFrequency = 500
  iMidiNote = ftom:i(iFrequency)
  print(iMidiNote)
endin

</CsInstruments>
<CsScore>
i "FTOM" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

You will see in the console:

    iMidiNote = 71.213


### "i" in the Score and "i-rate" ...

... have **nothing** to do with each other.

More in general: The score is not Csound code. The score is basically a list
of instrument calls, with some simple conventions. No programming language at all.

This is sometimes confusing for beginners. But in modern Csound often the score
remains empty. We will show in [Tutorial 07](01-GS-07.md) why this is possible.

