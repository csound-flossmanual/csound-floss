# 05 Hello MIDI Keys


## What you learn in this tutorial

- How to work with MIDI note numbers instead of raw frequency values.
- What *i-rate* variables in Csound are.
- How to print *i-rate* variables in the Csound console.



## Problems of using raw frequencies

In [Hello Frequency](01-GS-02.md) we created a line, or in musical terms,
a "glissando" from 500 Hz to 400 Hz:

    kFreq = linseg:k(500,0.5,400)

Actually we have two problems here:

1. When we ask a musician to play a certain pitch, we say "Can you please play
D", or "Can you please play D4". But what pitch is 500 Hz?

2. When we perform a glissando from 500 Hz to 400 Hz in the way we did, we do
it actually not in the linearity we thought we would.

We will discuss the first issue in the next paragraphs.  
For the second issue, you find a brief explanation below, in the optional part
of this tutorial. For more explanations, you may have a look at the
[Pitches](/basics/pitch-and-frequency#pitches) section in the 
[Ditigal Audio Basics](/basics/digital-audio) chapter of this book.


## MIDI note numbers and the mtof opcode

There are several systems and possibilities to specify pitches like *D4* instead
of raw frequencies in Csound.

I recommend using MIDI note numbers, because they are easy to learn 
and they are used also by other applications and programming languages.

All you must know about MIDI keys or note numbers: *C4* is set to note number
60. And then each semitone, or each next key on a MIDI keyboard, 
is *plus one* for upwards, and *minus one* for downwards.

OVERVIEW

If you want to transform any MIDI key to its related frequency, use the
`mtof` (midi to frequency) opcode.

When we want Csound to calculate the frequency which is related to *D4*,
and store the result in a variable, we write:

    iFreq = mtof:i(62)


## i-rate variables in Csound 

The MIDI key 62 is a number, not a signal. 
And so is the frequency which relates to this MIDI key.
It is a single number, not a signal.
This is the reason that we called this variable *__i__Freq*.

An **i-rate** variable in Csound is only calculated once: when the instrument
in which it occurs is initialized.  That is where the name comes from.

Remember that *k-rate* and *a-rate* variables are signals. A signal changes in 
time. This means that its values are re-calculated all the time. To summarize:

- **a-rate** variables are signals which are updated every sample.
- **k-rate** variables are signals which are updated every k-cycle. This is 
less often than the *a-rate*, and determined by the `ksmps` constant.
- **i-rate** variables are no signals but numbers which are only calculated 
once for each instrument call. They keep this value during the performance of 
this note and will not change it.



## The print opcode

Now we would like to know which frequency the *iFreq* variable holds.

For a programming language this means to *print*. "Printing" means: Show values
in the console. In the console we see the messages from the program.

The `print` opcode is what we are looking for. Its usage is:

    print(iVariable)
    
Please run the following code, and look in the console.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Print
  iFreq = mtof:i(62)
  print(iFreq)
endin

</CsInstruments>
<CsScore>
i "Print" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

You should see this message near the end of the console output:

    instr 1:  iFreq = 293.665

So we know that the frequency which is related to the MIDI key number 62,
or *D4*, is 293.665 Hz.

Note: The `print` opcode is **for i-rate variables only**. You can **not**
use this opcode for printing *k-rate* or *a-rate* variables.  
We will show opcodes for printing control- or audio-variables later.


## Example

We will use now MIDI note for the glissando, instead of raw frequencies.

We create a line which moves in half a second from MIDI note 72 (C5) to 
MIDI note 68 (Ab4). This line we store in the Variable *kMidi*:

    kMidi = linseg:k(72,0.5,68)

This line is a signal because it changes in time.

Afterwards we convert this line to frequencies:

    kFreq = mtof:k(kMidi)

Note that we use `mtof:k` here because we apply the midi-to-frequency
converter to the *k-rate* signal *kMidi*. The result is a *k-rate* signal, too.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kAmp = linseg:k(0.3,0.5,0.1)
  kMidi = linseg:k(72,0.5,68)
  kFreq = mtof:k(kMidi)
  aSine = poscil:a(kAmp,kFreq)
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~ 

## Try it yourself

Change the *kMidi* signal so that

1. the first MIDI key is *E5* rather than *C5*
2. the second MIDI key is *G4* rather than *Ab4*
3. the whole duration of the instrument is used for the *glissando*
4. the glissando goes upwards rather than downwards.

Change also this:

5. Create two variables *iFreqStart* and *iFreqEnd* for the the two MIDI notes.
Insert then these *i-variables* in the `kMidi = linseg(...)` line.
6. Code a "chromatic scale" (= using always the next MIDI note) which falls from
*D5* to *A4*. Each MIDI key stays for one second, and then immediately moves 
to the next step. You can get this with `linseg` by using zero as duration 
between two notes. The beginning will be: `kMidi = linseg:k(74,1,74,0,73,...)`  
Don't forget to adjust the overall duration in the score; otherwise you will not
hear the series of pitches although you created it ...


## Opcodes and terms you have learned in this tutorial

### Opcodes

- mtof:i(MIDI_note) MIDI-to-frequency converter for one MIDI note
- mtof:k(kMIDI_notes) MIDI-to-frequency converter for a signal

### Terms

- *i-rate* is the "time" (as point not as duration) in which an instrument is
initialized
- *i-rate variable* or *i-variable* is a variable which gets a value only
once, at the initialization of an instrument.


## Go on now ...

with the next tutorial: [06 Hello Decibel](01-GS-06.md).


## ... or read some more explanations here

### The same is not the same ...

It is worth to look a bit closer to the question of pitch line versus
frequency line. We have two possibilities when we create a *glissando* between
two MIDI notes:

1. We first create the line between the two MIDI notes. Afterwards we convert
this line to the frequencies. This is what we did in the example code:

~~~csound
kMidi = linseg:k(72,0.5,68)
kFreq = mtof:k(kMidi)
~~~

2. We first convert the two MIDI notes to frequencies. Afterwards we create
a line. This would be the code:

~~~csound
iFreqStart = mtof:i(72)
iFreqEnd = mtof:i(68)
kFreq = linseg:k(iFreqStart,0.5,iFreqEnd)
~~~

For better comparision, we change the code so that  
- we move 10 seconds instead of 0.5 seconds  
- we move two octaves instead of four semitones.

We choose *A5* (= 880 Hz or MIDI note 81) and *A3* (= 220 Hz or MIDI note 57) 
as start and end. And we create one variable for each way.

    kMidi = linseg:k(81,10,57)
    kFreqLine_1 = mtof:k(kMidi)
    iFreqStart = mtof:i(81)
    iFreqEnd = mtof:i(57)
    kFreqLine_2 = linseg:k(iFreqStart,10,iFreqEnd)

When we use one oscillator for each frequency line, we can listen to both
versions at the same time. Don't worry about the opcodes which you do not know
yet. The `prints` and `printks` statements are just there to show which value
the *kFreqLine_1* and *kFreqLine_2* have.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Compare

  kMidi = linseg:k(81,10,57)
  kFreqLine_1 = mtof:k(kMidi)
  
  iFreqStart = mtof:i(81)
  iFreqEnd = mtof:i(57)
  kFreqLine_2 = linseg:k(iFreqStart,10,iFreqEnd)
  
  prints("Time (sec)  kFreqLine_1  kFreqLine_2\n")
  printks("  %2d          %d           %d\n", 1,
          timeinsts(), kFreqLine_1, kFreqLine_2)
  
  aOut_1 = poscil:a(0.2,kFreqLine_1)
  aOut_2 = poscil:a(0.2,kFreqLine_2)
  aFadeOut = linen:a(1,0,p3,1)
  out(aOut_1*aFadeOut,aOut_2*aFadeOut)
  
endin

</CsInstruments>
<CsScore>
i "Compare" 0 11
</CsScore>
</CsoundSynthesizer>
~~~ 

In the console, you should see this:

~~~
Time    kFreqLine_1  kFreqLine_2
(sec)      (Hz)         (Hz)
 0         880          880
 1         766          814
 2         667          748
 3         580          682
 4         505          616
 5         440          550
 6         383          483
 7         333          417
 8         290          351
 9         252          285
10         220          220
~~~


### More to i-rate and k-rate

### "i" in the score and "i-rate"

### MIDI, Midi or midi?!?
