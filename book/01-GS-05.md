# 05 Hello MIDI Keys

### What you learn in this tutorial

- How to work with **MIDI note numbers** instead of raw frequency values.
- How to **convert** MIDI notes **to frequencies**.
- What **i-rate variables** in Csound are.
- How to **print i-rate variables** in the Csound console.

## Problems of Using Raw Frequencies

In [Hello Frequency](01-GS-02.md) we created a line, or in musical terms,
a _glissando_ from 500 Hz to 400 Hz:

    kFreq = linseg:k(500,0.5,400)

Actually we have two problems here:

1. When we ask a musician to play a certain pitch, we say "Can you please play
   D", or "Can you please play D4".
   But what pitch is 500 Hz?

2. When we perform a glissando from 500 Hz to 400 Hz in the way we did, we do
   it actually not in the linearity we thought we would.

We will discuss the first issue in the next paragraphs.
For the second issue, you find an explanation below, in the optional part
of this tutorial. You may also have a look at
the [Pitches](/basics/pitch-and-frequency#pitches) section in
the [Ditigal Audio Basics](/basics/digital-audio) chapter of this book.

## MIDI Note Numbers and the 'mtof' Opcode

There are several systems and possibilities to specify pitches like _D4_ instead
of raw frequencies in Csound.

I recommend using MIDI note numbers, because they are easy to learn
and they are used also by other applications and programming languages.

All you must know about MIDI keys or note numbers: _C4_ is set to note number 60.
And then each semitone, or each next key on a MIDI keyboard,
is _plus one_ for upwards, and _minus one_ for downwards.

![MIDI Note Numbers](../resources/images/01-GS-05-a.png)

If you want to transform any MIDI key to its related frequency, use
the `mtof` (midi to frequency) opcode.

When we want Csound to calculate the frequency which is related to _D4_,
and store the result in a variable, we write:

    iFreq = mtof:i(62)

## i-rate Variables in Csound

The MIDI key 62 is a number, not a signal.
And so is the frequency which relates to this MIDI key.
It is a single number, not a signal.
This is the reason that we called this variable **\*i**Freq\*.

An **i-rate** variable in Csound is only calculated **once**: when the instrument
in which it occurs is initialized. That is where the name comes from.

[Remember](/get-started/GS-02#k-rate-signals)
that **k-rate** and **a-rate** variables are **signals**. A signal varies in
time. This means that its values are re-calculated all the time. To summarize:

- **a-rate** variables are signals which are updated every sample.
- **k-rate** variables are signals which are updated every k-cycle. This is
  less often than the _a-rate_, and determined by the `ksmps` constant.
- **i-rate** variables are no signals but numbers which are only calculated
  once for each instrument call. They keep this value during the performance of
  the instrument's note and will not change it.

## The 'print' Opcode

Now we would like to know which frequency the _iFreq_ variable holds.

For a programming language this means to _print_. Via printing the program shows
values in the console. In the console we see the messages from the program.

The `print` opcode is what we are looking for. Its syntax is simple:

    print(iVariable)

Please run the following code, and look in the console.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Print
  iFreq = mtof:i(62)
  print(iFreq)
endin

</CsInstruments>
<CsScore>
i "Print" 0 0
</CsScore>
</CsoundSynthesizer>
```

You should see this message near the end of the console output:

    instr 1:  iFreq = 293.665

So we know that the frequency which is related to the MIDI key number 62,
or _D4_, is 293.665 Hz.

Note: The `print` opcode is **for i-rate variables only**.
You can **not** use this opcode for printing _k-rate_ or _a-rate_ variables.
We will show opcodes for printing control- or audio-variables later.

## Example

We will use now MIDI notes for the glissando, instead of raw frequencies.

We create a line which moves in half a second from MIDI note 72 (C5) to
MIDI note 68 (Ab4). This MIDI note line we store in the Variable _kMidi_:

    kMidi = linseg:k(72,0.5,68)

This line is a signal because it changes in time.

Afterwards we convert this line to frequencies by using the `mtof` opcode:

    kFreq = mtof:k(kMidi)

Note that we use `mtof:k` here because we apply the midi-to-frequency
converter to the _k-rate_ signal _kMidi_. The result is a _k-rate_ signal, too.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kAmp = linseg:k(0.3,0.5,0.1)
  kMidi = linseg:k(72,0.5,68)
  kFreq = mtof:k(kMidi)
  aSine = poscil:a(kAmp,kFreq)
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
```

## The 'prints' Opcode and Strings

So far we have only dealt with _numbers_.

We have discussed that numbers can only be calculated once, at _i-rate_,
or that they are calculated again and again for a block of samples, so
being _k-rate_, or even are calculated again and again for each sample,
so being _a-rate_.

But all are numbers.

This is somehow natural for an audio application, compared to a text program.

But even in an audio application we need something to write text in, for instance
when we point to a sound file as "myfile.wav".

This data type, which starts and ends with double quotes, is called a **string**.
(The term is from "character string", so a chain of characters.)

The `prints` opcode is similar to the `print` opcode, but it prints a string,
and not a number. Try this:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Prints
  prints("Hello String!\n")
endin

</CsInstruments>
<CsScore>
i "Prints" 0 0
</CsScore>
</CsoundSynthesizer>
```

Perhaps you wonder about the `\n` at the end of the string.

This is a format specifier. It adds a new line.

Please compare: Remove the two characters `\n` and run the code again. You will
see that the console printout is now immediately followed by the next Csound
message.

Or add another `\n` to the existing one, and you will see an empty line after
"Hello String!".

In fact, there are much mor format specifiers. We will get back to them
and to `prints` in [Tutorial 09](/get-started/GS-09#format-strings).

## Try it yourself

Change the _kMidi_ signal so that

1. the first MIDI key is _E5_ rather than _C5_
2. the second MIDI key is _G4_ rather than _Ab4_
3. the whole duration of the instrument is used for the _glissando_
4. the glissando goes upwards rather than downwards.

Change also this:

5. Create two variables _iFreqStart_ and _iFreqEnd_ for the the two MIDI notes.
   (You need to convert the MIDI notes at _i-rate_ for it.)
   Insert then these _i-variables_ in the `kFreq = linseg(...)` line.
   Compare the result to the one in the example.
6. Code a "chromatic scale" (= using always the next MIDI note) which falls
   from _D5_ to _A4_. Each MIDI key stays for one second, and then immediately moves
   to the next step. You can get this with `linseg` by using zero as duration
   between two notes. This is the start: `kMidi = linseg:k(74,1,74,0,73,...)`.
   Don't forget to adjust the overall duration in the score; otherwise you will not
   hear the series of pitches although you created it ...

## Opcodes and terms you have learned in this tutorial

### Opcodes

- `mtof:i(MIDI_note)` MIDI-to-frequency converter for one MIDI note
- `mtof:k(kMIDI_notes)` MIDI-to-frequency converter for a _k-rate_ signal
- `print(iVariable)` prints _i-rate_ variables to the Csound console
- `prints(String)` prints a string to the Csound console

### Terms

- _i-rate_ is the "time" (as point, not as duration) in which an instrument is
  initialized
- _i-rate variable_ or _i-variable_ is a variable which gets a value only
  once, at the initialization of an instrument.
- A _string_ is a sequence of characters, enclosed by double quotes.
  I'd recommend to only
  use [ASCII characters](https://en.wikipedia.org/wiki/ASCII) in
  Csound to avoid problems.

## Go on now ...

with the next tutorial: [06 Hello Decibel](01-GS-06.md).

## ... or read some more explanations here

### The same is not the same ...

It is worth to look a bit closer to the question of pitch line versus
frequency line. We have two possibilities when we create a _glissando_ between
two MIDI notes:

1. We first create the line between the two MIDI notes. Afterwards we convert
   this line to the frequencies. This is what we did in the example code:

```csound
kMidi = linseg:k(72,0.5,68)
kFreq = mtof:k(kMidi)
```

2. We first convert the two MIDI notes to frequencies. Afterwards we create
   a line. This would be the code:

```csound
iFreqStart = mtof:i(72)
iFreqEnd = mtof:i(68)
kFreq = linseg:k(iFreqStart,0.5,iFreqEnd)
```

For better comparision, we change the code so that

- we move during 12 seconds instead of 0.5 seconds
- we move two octaves instead of four semitones.

We choose _A5_ (= 880 Hz or MIDI note 81) and _A3_ (= 220 Hz or MIDI note 57)
as start and end. And we create one variable for each way.

    kMidiLine_1 = linseg:k(81,12,57)
    kFreqLine_1 = mtof:k(kMidi)

    iFreqStart = mtof:i(81)
    iFreqEnd = mtof:i(57)
    kFreqLine_2 = linseg:k(iFreqStart,12,iFreqEnd)

The _kFreqLine_1_ variable contains the frequency signal which derives from a linear
transition in the MIDI note domain. The _kFreqLine_2_ variable contains the
frequency signal which derives from a linear transition in the frequency domain.

When we use one oscillator for each frequency line, we can listen to both
versions at the same time. We will output the _kFreqLine_1_ in the left channel,
and the _kFreqLine_2_ in the right channel.

For more comparison, we also want to look at the MIDI note number version of
both signals. For _kFreqLine_1_, this is the _kMidiLine_1_ signal which we created.
But which MIDI pitches comply with the frequencies of the _kFreqLine_2_?

We can get these pitches via the `ftom` (frequency to midi) opcode. This opcode is
the reverse of the `mtof` opcode. For `ftom` we have frequency as input, and
get MIDI note numbers as output. So to get the corresponding MIDI pitches
to _kFreqLine_2_, we write:

    kMidiLine_2 = ftom:k(kFreqLine_2)

Here comes the code which plays both lines, and prints out MIDI and frequency values
of both lines, once a second. Don't worry about the opcodes which you do not know
yet. It is mostly about "pretty printing".

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 1
nchnls = 2
0dbfs = 1

instr Compare

  kMidi = linseg:k(81,12,57)
  kFreqLine_1 = mtof:k(kMidi)

  iFreqStart = mtof:i(81)
  iFreqEnd = mtof:i(57)
  kFreqLine_2 = linseg:k(iFreqStart,12,iFreqEnd)
  kMidiLine_2 = ftom:k(kFreqLine_2)

  prints("Time   Pitches_1  Freqs_1      Freqs_2  Pitches_2\n")
  prints("(sec)   (MIDI)     (Hz)          (Hz)     (MIDI)\n")
  printks("%2d      %.2f     %.3f      %.3f    %.2f\n", 1,
          timeinsts(), kMidi, kFreqLine_1, kFreqLine_2, kMidiLine_2)

  aOut_1 = poscil:a(0.2,kFreqLine_1)
  aOut_2 = poscil:a(0.2,kFreqLine_2)
  aFadeOut = linen:a(1,0,p3,1)
  out(aOut_1*aFadeOut,aOut_2*aFadeOut)

endin

</CsInstruments>
<CsScore>
i "Compare" 0 13
</CsScore>
</CsoundSynthesizer>
```

In the console, you should see this:

```console
Time   Pitches_1  Freqs_1      Freqs_2  Pitches_2
(sec)   (MIDI)     (Hz)          (Hz)     (MIDI)
 0      81.00     880.000      880.000    81.00
 1      79.00     783.991      825.000    79.88
 2      77.00     698.456      770.000    78.69
 3      75.00     622.254      715.000    77.41
 4      73.00     554.365      660.000    76.02
 5      71.00     493.883      605.000    74.51
 6      69.00     440.000      550.000    72.86
 7      67.00     391.995      495.000    71.04
 8      65.00     349.228      440.000    69.00
 9      63.00     311.127      385.000    66.69
10      61.00     277.183      330.000    64.02
11      59.00     246.942      275.000    60.86
12      57.00     220.000      220.000    57.00
```

When we plot the two frequency lines, we see that the first one looks like a
concave curve, whilst the second one is a straight line:

![Proportional vs. linear frequency transitions](../resources/images/01-GS-05-b.png)

We see that _Freqs_1_ reaches 440 Hz after half of the duration,
whilst _Freqs_2_ reaches 550 Hz after half of the duration.

The _Freqs_2_ line **subtracts** 55 Hz in each second of performance.

    880.000 - 55.000 = 825.000
    825.000 - 55.000 = 770.000
    ...
    275.000 - 55.000 = 220.000

The _Freqs_1_ line on the other hand has the same **ratio** between the
frequency values of two subsequent seconds:

    880.000 / 783.991 = 1.12246...
    783.991 / 698.456 = 1.12246...
    ...
    246.942 / 220.000 = 1.12246...

Our **perception** follows **ratios**. We will get back to this in
the [next tutorial](/get-started/GS-06).

What we hear is what the _Pitches_ printout shows:

![Pitch view of proportional vs. linear frequency transitions](../resources/images/01-GS-05-c.png)

We hear that the first line is constantly falling, whilst the second one is
too slow at the beginning and too fast at the end.

The dotted lines point to
MIDI note 69 which is one octave lower than the note at start.
This point is reached after
6 seconds for the _Pitches_1_ line. This is correct for a constant pitch
decay. We have two octaves which are crossed in 12 seconds, so each octave
needs 6 seconds. The _Pitches_2_ line, however, reaches MIDI note 69 after 8 seconds,
therefore needing 2/3 of the overall time to cross the first octave, and only 1/3
to cross the second octave.

To be written in muscial notation, this is how this "too slow" and "too fast"
look like:

![Linear frequency transitions in traditional notation](../resources/images/01-GS-05-d.png)

To adapt the MIDI notes to traditional notation, I have written the cent deviation
from the semitones above. The MIDI note number 79.88 for the second note is
expressed as A flat minus 12 Cent. If the deviations are larger than 14 Cent,
I have added an arrow to the accidentals.

It can be seen clearly how the interval of the first step is only slightly
bigger than a semitone, wheras the last step is nearly a major third.

If you enjoy this way of glissando, no problem. Just be conscious that it is
not as linear as its frequencies suggest.

### Standard pitch for MIDI and equal temperament

Keyboard instruments need one pitch as reference. This pitch is A4, and its
frequency is normally set to 440 Hz.

This was not always the case in european music tradition, and as far as I know
also in music traditions in other cultures. There was mostly a certain range in
which the standard pitch could vary. Even in the 19th century in which
scientific standardization prevailed more and more, this process continued.
The first international fixation of a standard pitch took place on a conference
in Vienna in 1885.

This standard pitch was 435 Hz. But orchestras have a tendency to rise the standard
pitch because the sound is more brilliant then. So finally 1939 on the conference
of the International Federation of the National Standardizing Association (ISA)
in London 440 Hz was fixed. This is valid until today, although most orchestras
play a bit higher, in 443 Hz.

Csound offers a nice possibility to change the standard pitch for MIDI. In the
orchestra header, you can, for instance, set the standard pitch to 443 Hz
via this statement:

    A4 = 443

If you don't set `A4`, it defaults to 440 Hz.

Once the standard pitch is set, all other pitches are calculated related to it.
The tuning system which MIDI uses, is the "equal temperament". This means that
from one semitone to the next the frequency ratio is always the same:
${\sqrt[12]{2}}$ which can also be written as $2^{1/12}$.

So if A4, which is MIDI note number 69, has 440 Hz, note number 70 will have
$440 \cdot 2^{1/12}$ Hz. We can use Csound to calculate this:

    iFreq = 440 * 2^(1/12)
    print(iFreq)

This prints:

    iFreq = 466.164

And indeed this complies with the result of the `mtof` opcode:

    iFreq = mtof:i(70)
    print(iFreq)

Which also prints:

    iFreq = 466.164

### "i" in the Score and "i-rate" ...

... have **nothing** to do with each other.

More in general: The score is not Csound code. The score is basically a list
of instrument calls, with some simple conventions. No programming language at all.

This is sometimes confusing for beginners. But in modern Csound the score often
remains empty. We will show in [Tutorial 07](01-GS-07.md) how this works.
