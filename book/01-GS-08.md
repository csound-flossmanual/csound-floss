# 08 Hello Schedule

### What you learn in this tutorial

- How to call instruments **without** using **the score**
- How to get the **number** of a **named instrument**
- How to read the **Csound Reference Manual**

## The .csd File Again and a Bit of Csound History

You learned in [Tutorial 02](/get-started/GS-02#the-csound-document-structure)
that the Csound _.csd_ file consists of three sections:

- The `CsOptions` section sets some general options which are used once you
  pressed the "Play" button. For instance, whether you want to render Csound in
  real-time to the sound card, or to write a sound file. Or which MIDI device
  to use, or how many messages to display in the console.
- The `CsInstruments` section contains the actual Csound code. It is also called
  the "Orchestra" code because it comprises the Csound instruments.
- The `CsScore` section collects score lines. Each score line which begins
  with `i` initiates an instrument instance, at a certain time, for a
  certain duration, and possibly with additional parameters.

On one hand, this is a reasonable division of jobs. In the `CsInstruments` we
code the instruments, in the `CsScore` we call them, and in the `CsOptions` we
determine some general settings about the performance. In early times of Csound,
the instrument definitions and the score resided in two seperated text files.
The instrument definitions, or orchestra, were collected in an _.orc_ file,
and the score lines were collected in a _.sco_ file. The options were put in
so called "flags" which you still see in the `CsOptions`. The `-o` flag, for
example, assigns the audio output.

It is still possible to use Csound in this way from
the "command line", or "Terminal". A Csound run would be started by a
command line like this one:

    csound -o dac -m 128 my_instruments.orc my_score_lines.sco

The call to the Csound executable is done with typing `csound` at first position.
In second position we can add as many options as we like. In this case:
`-o dac -m 128` for real-time output and reduced message level. At third and
fourth position then follow the _.orc_ and _.sco_ file.

This command line way of running Csound is still a very versatile option.
If you enjoy learning Csound, and continue using it for your own audio projects,
you will probably at any time in future use it, because it is fast and stable,
and you can integrate Csound in any scripting environment.

## Using Csound Without the Score Section

We came across this retrospective because the separation between an "orchestra"
and a "score" file in early Csound clearly shows the different role of both.

It is not only a different role. It can be said that an instrument knows a bit
about the score. As you learned in [Tutorial 04](/get-started/GS-04)
and [Tutorial 07](/get-started/GS-07), each instrument
instance knows about the _p-fields_ which created this instance. It knows about
its duration, it even knows its start time in the overall Csound performance,
and if called by a fractional number, it knows its instance as a unique number.

But the score knows nothing about the instruments. Not even what the sample rate
is, or how many channels we set in the orchestra header via `nchnls = 2`.
We cannot use any opcode in the score, nor any variable name. The score does
not understand the orchestra, and to put it in this way: The score does not
understand Csound language.

There are several situations, however, in which we need **one** language to do all.
We may want to start instruments from inside another instrument. We may want to
pass a variable to an instrument which we calculate during the performance.
We may want to trigger instrument instances from live input, like touchscreens,
MIDI keyboards or messages from other applications.

Csound offers this flexibility. The most used opcodes for calling instruments
from inside the `CsInstruments` section, are `schedule` and `schedulek`.
We will introduce at first `schedule`, and then in [Tutorial 11](/get-started/GS-11)
come to `schedulek`.

So from now on you will see the score section mostly empty. But of course there
are still many situations in which a traditional score can be used. You will
find some hints for score usage in the optional section of this tutorial.

## The 'schedule' Opcode

The `schedule` opcode has the same function as an `i` score line. It calls an
instance of a defined instrument It has at least three input arguments,
because each instrument call needs to contain

1. number or name of the called instrument
2. start time of the called instrument
3. duration of the called instrument.

This code calls instrument "Hello" at start time zero for two seconds:

    schedule("Hello",0,2)

As `schedule` is an opcode, its input arguments are **separated by commas**.
This is the main difference to the score in which the parameter fields are
separated by spaces:

    i "Hello" 0 2

Note 1: As you see the starting `i` in the score is omitted. This is possible
because `schedule` only instantiates _instrument_ events whilst a score line
can also have other statements. Have a look below in the optional part of this
tutorial if you want to know more about it.

Note 2: It is up to you whether you add spaces to the commas in the `schedule`
argument list, or not. For Csound, the commas are the separator. Once you accept this,
you can use spaces or tabs in addition, or not. So these two lines are both
valid:

    //arguments are separated by commas only
    schedule("Hello",0,2)
    //arguments are separated by commas followed by spaces
    schedule("Hello", 0, 2)

## Example

The following example simply transfers the score lines from
[Tutorial 07](/get-started/GS-07) to `schedule` statements.
So it will sound exactly the same as the example in Tutorial 07.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

//same instrument code as in tutorial 07
instr Hello
  iMidiStart = p4
  iMidiEnd = p5
  kDb = linseg:k(-10,p3/2,-20)
  kMidi = linseg:k(iMidiStart,p3,iMidiEnd)
  aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin
//followed by schedule statements rather than score lines
schedule("Hello", 0, 2, 72, 68)
schedule("Hello", 4, 3, 67, 73)
schedule("Hello", 9, 5, 74, 66)
schedule("Hello", 11, .5, 72, 73)
schedule("Hello", 12.5, .5, 73, 73.5)


</CsInstruments>
<CsScore>
//the score is empty here!
</CsScore>
</CsoundSynthesizer>
```

## Try it yourself

Make sure that you always stop and restart Csound when you move to the next
excercise item.

- You can put the `schedule` lines anywhere in the `CsInstruments` section.
  Just make sure it is **outside** the "Hello" instrument.
  Try putting the lines anywhere by copy-and-paste. You can also scatter
  them anywhere, and in any order. This will probably not improve the readability
  of your code, but for Csound it will make no difference.
- Put one of the `schedule` lines in the score. Csound will report an error,
  because the score does not understand Csound orchestra code.
- Put **all** `schedule` lines in the "Hello" instrument. This is not an error,
  but nothing will happen when you run Csound: There is no statement any more which
  invokes any instance of instrument "Hello".
- Internally, Csound converts all instrument names to positive integer numbers.
  You can get this number via the `nstrnum` opcode. Put the code
  `iWhatIsYourNumber = nstrnum("Hello")`
  anywhere in the `CsInstruments` section, for instance below the `schedule`lines.
  Print this number to the console.
- Once you know this number, replace the string "Hello" in the `schedule`lines
  by this number. The Csound performance should be identical.
- When we call instrument "Hello" five times, as we do in the example, we call
  five instances of this instrument. We can assign numbers to these instances in
  calling the instrument as fractional number. Rather than calling instrument 1,
  we will call instrument 1.1, 1.2, 1.3, 1.4, and 1.5.  
  Replace the first argument
  of `schedule` by these numbers and insert `print(p1)` into the instrument code
  to prove that the instrument received this information.
- Keep this code, but change the first argument of `schedule` in the way that
  you still work with the instrument name here. Convert the instrument name "Hello"
  to a number via `nstrnum` and then add 0.1, 0.2, ... 0.5 for the five lines.
  Again the console output will be the same.

## Csound runs and runs and runs ...

When we use a traditional score, as we did in
[Tutorial 07](/get-started/GS-07#example), Csound will
terminate after the last score event.

But when we leave the score empty, Csound will not terminate. To put it in
an anthropomorphistic way: Csound waits. Imagine we have established a network
connection and can communicate with this Csound instance, then we might call
again the "Hello" instrument this way. Or if we have a MIDI keyboard connected,
we can do the same.

So usually we want this "endless" performance. (According to the
[Csound Manual](https://csound.com/docs/manual/ScoreTop.html) this is about
nine billion years on a 64-bit machine ...) But in case we do want Csound to
terminate after a certain amount of time, we can put one line in the score:
An `e` followed by a space and then a number. Csound will stop after this
number of seconds. Please insert `e 20` in the `CsScore` section of our example
above, and run it again. Now it should stop after 20 seconds.

## The Csound Reference Manual as Companion and Field for Improvements

All information about the usage of an opcode is collected in the
[Csound Reference Manual](https://csound.com/docs/manual/index.html).

Every opcode has an own page here. Please have a look at the page for the
`schedule`opcode which is at
[csound.com/docs/manual/schedule.html](https://csound.com/docs/manual/schedule.html).

You will see that the native Csound way of writing code is used here.
Rather than

    schedule(insnum, iwhen, idur [, ip4] [, ip5] [...])

it is written

    schedule insnum, iwhen, idur [, ip4] [, ip5] [...]

This is not a big difference. In the native Csound syntax, you will write
the input arguments of an opcode at right hand side, and the output of an opcode
left hand side; without any `=` in between.

As the `schedule` opcode has only input arguments, we have nothing at left hand
side.

To get used to this way of writing, let us look at the `linseg` manual page. You
find it [here](https://csound.com/docs/manual/linseg.html) in the Reference
Manual, and this is its information about the syntax of `linseg`:

    ares linseg ia, idur1, ib [, idur2] [, ic] [...]
    kres linseg ia, idur1, ib [, idur2] [, ic] [...]

In the functional style of coding which I use in this tutorial, it would read:

    ares = linseg:a(ia, idu1, ib  [, idur2] [, ic] [...])
    kres = linseg:k(ia, idu1, ib  [, idur2] [, ic] [...])

You will find detailed information in these Reference pages. Some of them may
be too technical for you. You will also find a working example for each opcode
which is very valuable to get an impression of what this opcode can do.

You may also read something on one of these pages which is outdated.
For an Open Source Project it is always a major
issue to keep the documentation up to date. We all are asked to contribute,
if we can, for instance in opening a ticket at
[Github](https://github.com/csound/manual/issues) or by suggesting an improvement
of the Reference Manual to the [Csound community](https://csound.com/community).

## Opcodes you have learned in this tutorial

### Opcodes

- `schedule` calls an instrument instance similar to an `i` score line
- `nstrnum` returns the internal Csound number of an instrument name

## Go on now ...

with the next tutorial: [09 Hello If](01-GS-09.md).

## ... or read some more explanations here

### Score statements and utilities

Before we leave the score, we should at least mention some of its facilities.

It must be said that for the ones who write a fixed media piece, the score
offers a lot of useful and proven tools.

So far we only used the `i` statement which calls an instrument instance, and
which we somehow replaced in this tutorial by the `schedule` opcode in the
`CsInstruments` section.

We also mentioned the `e` statement which terminates Csound after a certain time.

Another useful statement is the `t` or "Tempo" statement which sets the time
for one beat, in metronome units. Per default, this is 60, which means that one
beat equals one second. But it can be set to other values; not only once
for the whole performance but
having different metronome values at different times, and also interpolating
between them (resulting in becoming faster or becoming slower).

Here is an overview, with links to detailed descriptions, in the Csound
Reference Manual:
[csound.com/docs/manual/Scorestatements.html](https://csound.com/docs/manual/ScoreStatements.html)

And in this book, the Csound FLOSS Manual, we have a chapter about
[methods of writing scores](14-a-methods-of-writing-csound-scores.md), too.

Steven Yi's [Blue](https://blue.kunstmusik.com) frontend for Csound offers
sophisticated possibilities of working with score events as objects.

### Triggering other score events than 'i' from the orchestra code

If you need to trigger another score event than an instrument event, you can
use the `event_i` opcode, and its _k-rate_ version `event`.
You should not expect everything to work, because
the Csound score is preprocessed before the Csound performance starts. This is
not possible when we fire a score event from inside the orchestra. So the
`t` statements and similar will not work.

The `e` statement, however, will work, and can be used to terminate Csound
in a safe way at any time of the performance. In this example, we call at first
the _Play_ instrument which plays a sine tone for three seconds. Then we call the
_Print_ instrument which displays its message in the console and calls the
_Terminate_ instrument after three seconds. This instrument then terminates
the Csound performance.

```csound
<CsoundSynthesizer>
<CsOptions>
-m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

instr Play
  aSine = poscil:a(.2,444)
  outall(linen:a(aSine,p3/2,p3,p3/2))
endin
schedule("Play",0,3)

instr Print
  puts("I am calling now the 'e' statement after 3 seconds",1)
  schedule("Terminate",3,0)
endin
schedule("Print",3,1)

instr Terminate
  event_i("e",0)
endin

</CsInstruments>
<CsScore>
</CsScore>
</CsoundSynthesizer>
```

### Three Particular Durations: 0, -1 and z

We have already used sometimes in examples the duration 0. For instance:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

instr Zero
  prints("Look at me!\n")
endin

</CsInstruments>
<CsScore>
i "Zero" 0 0
</CsScore>
</CsoundSynthesizer>
```

When you look at the console output, you see `Look at me!`.

So the instrument instance has been called, although there is "no duration"
in the score line: The third parameter is `0`.

For Csound, calling an instrument with duration zero means: Only execute the
initialization pass. In other words: All _i-rate_ statements will work, but
no _k-rate_ or _a-rate_ one.

The other particular duration is `-1`. This was introduced for "unlimited" (held)
duration and "tied" notes. Important to know if we want to use `-1` as duration:

- An instrument called with a negative **p3** will run endlessly.
- But only one instance (!).
- We can turn off this instance by sending a score event with negative **p1**.

This is a simple example:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

instr 1
  prints("I am there!\n")
  iMidiNote = p4
  aSound = poscil:a(.2,mtof:i(iMidiNote))
  aOut = linenr:a(aSound,0,1,.01)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i 1 0 -1 70
i 1 1 -1 76
i -1 10 0 0
</CsScore>
</CsoundSynthesizer>
```

We hear that when the second instance starts after one seconds, the first
instance is "pushed out" brutally. Csound assumes that we want to continue
a _legato_ line; so no reason for more than one instance at the same time.

After ten seconds, however, the held note is finished gracefully by the
last score event, having `-1` as first parameter. The fade-out is done here
with the `linenr` opcode. We will explain more about it when we get to
realtime MIDI input.

If we want an instrument to play "endlessly", we can use the `z` character
as special symbol for the duration. According to the
[Csound Reference Manual](https://csound.com/docs/manual/ScoreTop.html),
it causes the instrument to run "approximately 25367 years".

Well, I do not dare to doubt this. But in this case, after ten seconds I started another
instrument whichs turns off all instances of instrument "Zett". We will use
`turnoff2` or `turnoff2_i` more later.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac -m128
</CsOptions>
<CsInstruments>
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

instr Zett
  prints("I am there!\n")
  iMidiNote = p4
  aSound = poscil:a(.1,mtof:i(iMidiNote))
  aOut = linenr:a(aSound,0,3,.01)
  outall(aOut)
endin

instr Turnoff
  turnoff2_i("Zett",0,1)
endin

</CsInstruments>
<CsScore>
i "Zett" 0 z 70
i "Zett" 1 z 76
i "Zett" 4 z 69
i "Turnoff" 10 0
</CsScore>
</CsoundSynthesizer>
```

Please note that `z` can **only** be used in the **score**. If we use it
in a `schedule()` call, it will be interpreted as a variable name.
