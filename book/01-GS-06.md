# 06 Hello Decibel


## What you learn in this tutorial

- How to convert Decibel values to amplitudes.
- How to use an expression as input argument without creating a variable.


## Problems of using raw amplitudes

In the last tutorial, we discussed some issues in working with raw frequencies,
and that it is usually preferred to work with MIDI note numbers.

There is a similar issue in working with raw amplitude values. Human perception
of both, pitch and volume, follows *ratios*. We hear the frequencies in the left
column as octaves because they all have the ratio 2 over 1:

    Frequency         Ratio
     8000 Hz
             > 8000 Hz / 4000 Hz = 2:1
     4000 Hz
             > 4000 Hz / 2000 Hz = 2:1
     2000 Hz
             > 2000 Hz / 1000 Hz = 2:1
     1000 Hz
             > 1000 Hz / 500 Hz = 2:1
      500 Hz
             > 500 Hz / 250 Hz = 2:1
      250 Hz
             > 250 Hz / 125 Hz = 2:1
      125 Hz

In the same way, we perceive these amplitudes as having equal loss in volume,
because they all follow the same ratio:

    Amplitude     Ratio
        1
             >  1 / 0.5 = 2
       0.5
             >  0.5 / 0.25 = 2
       0.25
             >  0.25 / 0.125 = 2
       0.125
             >  0.125 / 0.0625 = 2
       0.0625

As you see, already after four "intensity octaves" we get to an amplitude
smaller than 0.1. But human hearing is capable of about fifteen of these
intensity octaves!

It is the **Decibel** (dB) scale which reflects this. As you already know from
[Tutorial 2](01-GS-02.md), we set the amplitude 1 as reference value to
zero dB by this statement in the orchestra header:

    0dbfs = 1
    
Zero dB means here: The highest possible amplitude. Each amplitude ratio of
one over two is then a loss of about 6 dB. This yields the following relations
between amplitudes and decibels:

    Amplitude    dB
    1             0
    0.5          -6
    0.25        -12
    0.125       -18
    0.063       -24
    0.0316      -30
    0.01585     -36
    0.00794     -42
    0.00398     -48
    0.001995    -54
    0.001       -60

Note 1: To be precise, for an amplitude ratio of 1/2 the difference is
-6.0206 dB rather than -6 dB. So the amplitude column is not following precisely
the ratio 1/2.

Note 2: You can find more about sound intensities 
[here](16-c-intensities.md) in this book.

Note 3: For the general context you may have a look at the 
[Weber-Fechner law](https://en.wikipedia.org/wiki/Weber%E2%80%93Fechner_law).



## The ampdb opcode

So we will usually like to work with Decibel rather than with raw amplitudes. 
As the oscillator requires an amplitude as input argument, we must convert 
decibel to amplitudes. This is done via the `ampdb` (amplitude from decibel) 
opcode. Run this, and have a look at the console output:

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Convert
  iAmp = ampdb:i(-6)
  print(iAmp)
endin

</CsInstruments>
<CsScore>
i "Convert" 0 0
</CsScore>
</CsoundSynthesizer>
~~~

You should see this message:

    instr 1:  iAmp = 0.501

Similar to `mtof`, the `ampdb` opcode can run at *i-rate* or at *k-rate*.
Here we use *i-rate*, so `ampdb:i` because we have a number as input, and 
not a signal.

We will use `ampdb:k` when we have a rise or a decay in volume. We can create
this as usual with the `linseg` opcode. Here is a signal which moves from -10 dB
tp -20 dB in half a second:

    kDb = linseg:k(-10,0.5,-20)

And this is the following conversion to amplitudes:

    kAmp = ampdb:k(kDb)


## Inserting an expression as input argument

So far we have always stored the output of an opcode in a variable; The output
of an opcode gets a name, and this name is then used as input for the next
opcode in the chain. We have currently four chain links. These chain links are 
written as numbers right hand side in the next figure.

![Input-output-chain with variables](../resources/images/01-GS-06-a.png)

It is possible to omit the variable names and to directly pass one expression
as input argument into the next chain link. This is the code to skip the variable
names for chain link 2:

    aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))

And the figure can be drawn like this:

![Direct connection at chain link 2](../resources/images/01-GS-06-b.png)


## Example

This version is used in the example code.

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kDb = linseg:k(-10,0.5,-20)
  kMidi = linseg:k(72,0.5,68)
  aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~
 

## Try it yourself

- Change the code so that you create the variable names *kAmp* and *kFreq*
first, as shown in the first figure. (You will get two more lines of code.)
- Play with omitting the variable names also in chain link 1, 3 or 4. Which
version do you like most?


## Opcodes you have learned in this tutorial

- `ampdb:i(iDecibel)` converts a Decibel number to an amplitude number
- `ampdb:k(kDecibel)` converts a Decibal signal to an amplitude signal

## Go on now ...

with the next tutorial: [07 Hello p-Fields](01-GS-07.md).


## ... or read some more explanations here

### Short or readable?


