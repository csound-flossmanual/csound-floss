# 06 Hello Decibel

### What you learn in this tutorial

- How **human perception** follows **ratios** not only in frequencies
  but also in **amplitudes**.
- How the **Decibel (dB)** scale works.
- How to **convert Decibel** values to **amplitudes**.
- How to use an **expression** as **input argument** without creating a variable.

## Problems of Using Raw Amplitudes

In the last tutorial, we discussed some issues in working with raw frequencies,
and that it is usually preferred to work with MIDI note numbers.

There is a similar issue in working with raw amplitude values. Human perception
of both, pitch and volume, follows _ratios_. We hear the frequencies in the left
column as octaves because they all have the ratio 2 over 1:

    Frequency         Ratio
     8000 Hz
             > 8000 Hz / 4000 Hz = 2:1
     4000 Hz
             > 4000 Hz / 2000 Hz = 2:1
     2000 Hz
             > 2000 Hz / 1000 Hz = 2:1
     1000 Hz
             > 1000 Hz / 500 Hz = 2:1
      500 Hz
             > 500 Hz / 250 Hz = 2:1
      250 Hz
             > 250 Hz / 125 Hz = 2:1
      125 Hz

In the same way, we perceive these amplitudes as having equal loss in volume,
because they all follow the same ratio:

    Amplitude     Ratio
        1
             >  1 / 0.5 = 2
       0.5
             >  0.5 / 0.25 = 2
       0.25
             >  0.25 / 0.125 = 2
       0.125
             >  0.125 / 0.0625 = 2
       0.0625

As you see, already after four "intensity octaves" we get to an amplitude
smaller than 0.1. But human hearing is capable of about fifteen of these
intensity octaves!

## Decibel

It is the **Decibel** (dB) scale which reflects this. As you already know from
[Tutorial 2](01-GS-02.md), we set the amplitude 1 as reference value to
zero dB by this statement in the orchestra header:

    0dbfs = 1

Zero dB means here: The highest possible amplitude. Each amplitude ratio of
one over two is then a loss of about 6 dB. This yields the following relations
between amplitudes and decibels:

    Amplitude    dB
    1             0
    0.5          -6
    0.25        -12
    0.125       -18
    0.063       -24
    0.0316      -30
    0.01585     -36
    0.00794     -42
    0.00398     -48
    0.001995    -54
    0.001       -60

Note 1: To be precise, for an amplitude ratio of 1/2 the difference is
-6.0206 dB rather than -6 dB. So the amplitude column is not following precisely
the ratio 1/2.

Note 2: You can find more about sound intensities
[here](16-c-intensities.md) in this book.

Note 3: For the general context you may have a look at the
[Weber-Fechner law](https://en.wikipedia.org/wiki/Weber%E2%80%93Fechner_law).

## The 'ampdb' Opcode

So we will usually like to work with Decibel rather than with raw amplitudes.
As the oscillator requires an amplitude as input argument, we must **convert
decibel to amplitudes**. This is done via the `ampdb` (amplitude from decibel)
opcode. Run this, and have a look at the console output:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Convert
  iAmp = ampdb:i(-6)
  print(iAmp)
endin

</CsInstruments>
<CsScore>
i "Convert" 0 0
</CsScore>
</CsoundSynthesizer>
```

You should see this message:

    instr 1:  iAmp = 0.501

Similar to `mtof`, the `ampdb` opcode can run at _i-rate_ or at _k-rate_.
Here we use _i-rate_, so `ampdb:i` because we have a number as input, and
not a signal.

We will use `ampdb:k` when we have time varying decibel values as input.
In the most simple case, it is a linear rise or decay. We can
create this input signal as usual with the `linseg` opcode.
Here is a signal which moves from -10 dB to -20 dB in half a second:

    kDb = linseg:k(-10,0.5,-20)

And this is the following conversion to amplitudes:

    kAmp = ampdb:k(kDb)

## Inserting an Expression as Input Argument

So far we have always stored the output of an opcode in a variable; The output
of an opcode gets a name, and this name is then used as input for the next
opcode in the chain. We have currently four chain links. These chain links are
written as numbers right hand side in the next figure.

![Input-output-chain with variables](../resources/images/01-GS-06-a.png)

It is possible to omit the variable names and to directly pass one expression
as input argument into the next chain link. This is the code to skip the variable
names for chain link 2:

    aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))

And the figure can be drawn like this:

![Direct connection at chain link 2](../resources/images/01-GS-06-b.png)

## Example

This version is used in the example code.

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kDb = linseg:k(-10,0.5,-20)
  kMidi = linseg:k(72,0.5,68)
  aSine = poscil:a(ampdb:k(kDb),mtof:k(kMidi))
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
```

## Short or readable?

The possibility of directly inserting the output of one opcode in another
is potenially endless. It leads to shorter code.

On the other hand, if many of these expressions are put inside each other,
the code can become a stony desert of `(`, `:`, `,` and `))`.

Variables are actually not a necessary evil. They can be a big help in
understanding what happens in the code, if they carry a meaningful name.
This is what I tried as setting _kMidi_, _aSine_, ... Perhaps you will find
better names; that is what we should try.

In case we insert all four chain links into each other, the code would look like
this:

    outall(linen:a(poscil:a(ampdb:k(linseg:k(-10,.5,-20),...)

I don't think this makes sense; in particular not for beginners. In the
Csound FLOSS Manual we have a hard limit because of layout: No code line
can be longer than 78 characters. I think this is a good orientation.

The most valuable qualities of code are clarity and readability. If direct
insertion of an expression helps for it, then use it. But better
to avoid code abbreviations which more derive from laziness than from decision.

You yourself are the best judge. Read your code again after one week,
and then make up your mind ...

## Try it yourself

- Create a _cresendo_ (a rise in volume) rather than a _diminuendo_
  (a decay in volume) as we did.
- Change the values so that the _crescendo_ becomes more extreme.
- Again change to _diminuendo_ but also more extreme than in the example.
- Change the code so that you create the variable names _kAmp_ and _kFreq_
  first, as shown in the first figure.
- Play with omitting the variable names also in chain link 1, 3 or 4. Which
  version do you like most?

## Opcodes you have learned in this tutorial

- `ampdb:i(iDecibel)` converts a Decibel number to an amplitude number
- `ampdb:k(kDecibel)` converts a Decibal signal to an amplitude signal

## Go on now ...

with the next tutorial: [07 Hello p-Fields](01-GS-07.md).

## ... or read some more explanations here

### What is 0 dB?

We have stressed some similarities between working with pitch and with volumes.
The similarities derive from the fact that we perceive in a proportional way by
our senses.
Both, the MIDI scale for pitch and the Decibel scale for volume, reflect this.

But there is one big difference between both. The MIDI scale is an **absolute**
scale. MIDI note number 69 **is** 440 Hz. (Or slightly more cautious: MIDI note
number 69 is set to the standard pitch which is usually 440 Hz.)

But the Decibel scale is a **relative** scale. It does not mean anything to
say "this is -6 dB" unless we have **set** something as **0 dB**.

In **acoustics**, 0 dB is set to a very small value. To put it in a non-scientific
way: The softest sound which we can hear.

This means that all common Decibel values are then **positive**, because they are
(much) more than this minimum. For instance around 60 dB for a normal conversation.

But as explained above, in **digital audio** it is the other way round.
Here our **0 dB** setting points to the **maximum**, to the highest possible amplitude.

In digital audio, we have a certain number of bits for each sample: 16 bit, 24
bit, 32 bit. Whatever it be, there is a maximum. Imagine a 16 bit digital number
in which each bit can either be 0 or 1. The maximum possible amplitude is
when all bits are 1.

And so for the other resolutions. (They do not add something on top. They offer
a finer resolution between the maximum and the minimum.)

So it makes perfect sense to set this maximum possible amplitude as 0 dB.
But this means that in digital audio we only have **negative** dB values.

### Can I use positive dB values?

There is one exception. Yes it is true that there can be no amplitude larger
than 0 dB. But we can use positive dB in digital audio when we **amplify**
soft sounds.

As we saw in [Tutorial 04](/get-started/GS-04#volume-change-as-multiplication),
to amplify a signal means to multiply it by a value larger than 1. It makes
perfect sense to express this in decibel rather than as a multiplier.

We can say: "I amplify this signal by 6 dB", rather than: "I amplify it by
factor 2." And: "I amplify this signal by 12 dB" should be better than "I
amplify it by factor 4".

In this context we will use a positive number as input for the `ampdb` opcode.
Here is a simple example:

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Amplify
  //get dB value from fourth score parameter
  iDb = p4
  //create a very soft pink noise
  aNoise = pinkish(0.01)
  //amplify
  aOut = aNoise * ampdb(iDb)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Amplify" 0 2 0  //soft sound as it is
i "Amplify" 2 2 10 //amplification by 10 dB
i "Amplify" 4 2 20 //amplification by 20 dB
</CsScore>
</CsoundSynthesizer>
```

### A Look at Arithmetic and Geometric series

It is often quite interesting to look back from electronic music to some very
old traditions which are inherited in it. I'd like to undertake an excursion
to one of these traditions here. Just jump to the next tutorial if you are not
interested in it.

What we discussed in this and the previous tutorial about _linear_ versus
_proportional_ transitions in both, frequency and amplitude, has been described
by ancient greek mathematicians as _arithmetic_ versus _geometric_ series.

If we have two numbers, or lengths, and look after the one "in between", the
**arithmetic** mean searches for the equal **distance** between the smaller
and the larger one. Or in the words of Archytas of Tarentum (early 4th century
B.C.):

> The arithmetic mean is when there are three terms showing successively the
> same excess: the second exceeds the third by the same amount as the first exceeds
> the second. In this proportion, the ratio of the larger numbers is less,
> that of the smaller numbers greater.
> ^[Ancilla to the Pre-Socratic Philosophers. A complete translation of the Fragments in Diels,
> Fragmente der Vorsokratiker by Kathleen Freeman. Cambridge, Massachusetts: Harvard University Press [1948],
> quoted after http://demonax.info/doku.php?id=text:archytas_fragments]

If the first number is $8$, and the third number is $2$, we look for the
second number as arithmetic mean $A$ and the "excess" $x$ like this:

<p style="padding: 0.5em 2em; font-size: 12pt;">
$$
A = x + 2 \\
8 = x + A \\
2 + x + x = 8 \\
x + x = 6 \\
x = 3
$$
</p>

So the arithmetic mean $A$ is $5$, as $2+3= 5$ and $5+3=8$.

But as Archytas states, "the ratio of the larger numbers is less,
that of the smaller numbers greater". Here:

<p style="padding: 0.5em 2em; font-size: 12pt;">
$$
8 / 5 = 1.6 \\
5 / 2 = 2.5
$$
</p>

![Arithmetic mean of 8 and 2](../resources/images/01-GS-06-c.png)

This is what we described as "at first too slow then too fast" in the
[previous tutorial](/get-started/GS-05#the-same-is-not-the-same).

Here comes how Archytas describes the **geometric** mean:

> The geometric mean is when the second is to the third as the first is to the
> second; in this, the greater numbers have the same ratio as the smaller numbers.

So if we look for the geometric mean $G$ between $8$ and $2$ we calculate:

<p style="padding: 0.5em 2em; font-size: 12pt;">
$$
8 / G = G / 2 \\
8 = G \cdot G / 2 \\
16 = G^2 \\
G = 4
$$
</p>

![Geometric mean of 8 and 2](../resources/images/01-GS-06-c1.png)

The geometric mean of $8$ and $2$ is $4$ because the ratio of the larger number to the
mean, and the ratio of the mean to the smaller number is the same: $8/4=2$ and $4/2=2$.

It is quite interesting to look at the "geometric" way to construct this mean
which is shown in Euclid's _Elements_ (VI.8):

> If, in a right-angled triangle, a (straight-line) is drawn
> from the right-angle perpendicular to the base then the
> triangles around the perpendicular are similar to the
> whole (triangle), and to one another.
> ^[Euclid's Elements of Geometry, Translation R. Fitzpatrick, p. 164, cited after
> > https://farside.ph.utexas.edu/Books/Euclid/Elements.pdf]

![Euclid's construction of the geometric mean](../resources/images/01-GS-06-d.png)

Euclid describes how the two triangles which are left and right to this
perpendicular have the same angles, and that this is also the case when we
look at the large triangle. It establishes the perfect similarity.

The length of this perpendicular is the geometric mean of the two parts on the base.
According to the right triangle altitude theorem, the square of this altitude
equals the product of the base parts:

<p style="padding: 0.5em 2em; font-size: 12pt;">
$$
b^2 = a \cdot c \\
b = \sqrt{a \cdot c}
$$
</p>

Exactly this is the formular for the geometric mean.

There is also a close relationship to the **golden ratio** which is famous for
its usage in art and nature. In terms of the triangle which Euclid describes
it means: Find a triangle so that the smaller base part plus the height equal
the larger base part:

![Golden ratio as geometric mean](../resources/images/01-GS-06-e.png)

Currently we have $a = 8$, $c = 2$ and $b = 4$.  
Obviously, $8 = 4 + 2$ is not true.

We can move the seperation point a bit to the left, so that $a = 7$ and $c = 3$.  
This yields $7 = \sqrt{21} + 3$ which is also not true.

Bad luck: The golden ration can be easily
[constructed geometrically](https://en.wikipedia.org/wiki/Golden_ratio#Construction),
but it is an irrational number. We are close though when we choose higher
[Fibonacci numbers](https://en.wikipedia.org/wiki/Fibonacci_number). For
instance, for the Fibonacci numbers $b = 88$ and $c = 55$ it yields:  
$a = b \cdot {b/c} = 88 \cdot 88/55 = 140.8$ rather than the desired $143$.

Good enough for music perhaps which always needs some dirt for its life ...
