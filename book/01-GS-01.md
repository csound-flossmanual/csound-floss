# 01 Hello Csound

## What you learn in this tutorial

- How to create and output a sine tone.
- What Csound opcodes are.
- What audio rate in Csound means, and what an audio variable is.
- How a Csound .csd document is structured.



## What is a Sine Oscillator
A sine wave can be seen as the most elementary sound in the world.
When we draw a sine wave as a graph showing amplitude over time, 
it looks like this:

![Sine wave snake](../resources/images/01-GS-01-sine.png)

To produce a sine wave, Csound uses an oscillator.
An oscillator needs two informations as input:  

1. Which is the maximum amplitude to output. This results in louder or softer tones.  

2. How many periods (cycles) per second to create. This results in higher or
   lower pitches.  
   The unit is *Hertz (Hz)*. 1000 Hz would mean that a sine 
   has 1000 periods in each second.  


## A Sine Oscillator in Csound: Opcode and Arguments
Csound has many different oscillators.
(You can find [here](02-opcodes.md) some descriptions and comparisions.)

In this example, we use the opcode `poscil` which means "precise oscillator".  
An **opcode** is a processing unit in Csound, like an "object" in PureData or Max,
or a "UGen" in SuperCollider.  
If you are familiar with programming languages, you can see an opcode as a
built-in function.

The inputs of an opcode are called **arguments** and are written in parentheses
immediately after the opcode name.  
So `poscil(0.2,400)` means: The opcode `poscil` gets two input arguments.  
The first argument is the number `0.2`.  
The second argument is the number `400`.  
The meaning of the input arguments depends on how the opcode is implemented.
For `poscil`, the first input is the amplitude, and the second input is the
frequency. The [Csound Reference Manual](https://csound.com/docs/manual/index.html) 
contains the information about it.
We will give in [Tutorial XYZ]() some help how to use it.  
This way of writing code is very common in programming languages, like `range(13)` 
in [Python](https://en.wikipedia.org/wiki/Python_(programming_language)),
or `printf("no no")` in 
[C](https://en.wikipedia.org/wiki/C_(programming_language)), or `Date.now()` in
[JavaScript](https://en.wikipedia.org/wiki/JavaScript) (in the latter case with 
empty parentheses which means: no input arguments).

Note: There is also another way of writing Csound code. See 
[below](#traditional-and-functional-way-to-write-csound-code) 
if you want to learn more about it.  


## A Signal Flow and its Code
We create now a sine wave of 0.2 amplitude and 400 cycles per second (Hz).  
We will call this signal *aSine* because it is an audio signal. The character **a** 
at the beginning of the variable name signifies exactly this.  
An audio signal is a signal which produces a new value every sample.
(Learn more [here](16-a-digital-audio.md) about samples and sample rate.)  
This is the code line in Csound to produce the *aSine* signal:

    aSine = poscil:a(0.2,400)
	
This means: The signal *aSine* is created by the opcode `poscil` at audio rate
(`:a`), and the input for `poscil` is 0.2 for the amplitude and 400 for the 
frequency.

To output the signal (so that we can hear it), we put it in the `outall` 
opcode. This opcode sends an audio signal to all available output channels.

    outall(aSine)
	
Note that the signal *aSine* at first was the output of the oscillator, and 
then became input of the `outall` opcode. This is a typical chain which is 
well knows from modular synthesizers: A cable connects the output of one 
module with the input of another.

We can draw the program flow like this:

![Signal flow](../resources/images/01-GS-01-a.png)

In the middle you see the signal flow, with symbols for the oscillator and 
the output. You can imagine this as modules of a synthesizer, connected by a 
cable called *aSine*.  
Left hand side you see the chain between input, opcode and output. Note that
the output of the first chain, contained in the *aSine* variable, becomes the
input of the second chain.  
Right hand side you see the Csound code. Each line of code represents one
*input -> opcode -> output* chain, in the form *output = opcode(input)*.
The line `outall(aSine)` does not have an output in Csound, because it sends 
audio to the hardware (similar to the "dac~" object in PD or Max).


## Your first Csound instrument
In Csound, all oscillators, filters, sample players and other processing units
are encapsulated in an **instrument**. An instrument has the keyword 
`instr` at its start, and `endin` at its end. 

After the keyword `instr`, seperated by a space, we assign a number (1, 2, 3, ...) or a 
name to the instrument. Let us call our instrument "Hello", and include the 
code which we developed:


    instr Hello
      aSine = poscil:a(0.2,400)
      outall(aSine)
    endin


## Example

We are now ready to run the code. All we have to do is, to put the 
instrument in a complete Csound file.

Look at the code in the example. Can you retrieve the instrument code?

Push the "Play" button. You should hear two seconds of a 400 Hz sine tone.

Can you see why it plays for two seconds?

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  aSine = poscil:a(0.2,400)
  outall(aSine)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~

## The Csound Score

At the bottom of the example code, you find this:


    <CsScore>
    i "Hello" 0 2
    </CsScore>
    

This is the **Score** section of the .csd File.
it starts with the tag `<CsScore>` and ends with `</CsScore>`.
In between there is this score line:

    i "Hello" 0 2
	
The `i` means: This is an instrument event.  
The `"Hello"` means: It is about the "Hello" instrument.  
The `0` means: Start time of this instrument is 0 (= start immediately).  
The `2` means: The duration of this instrument is 2 (seconds).


## The Csound Document Structure

The whole Csound document consists of three parts:

1. The `<CsOptions>` tag. You see here the statement:
	`-o dac`
	This means: The output (`-o`) shall be written to the digital-to-analog converter
	(`dac`); in other words: to the sound card. Because of this, we listen to 
	the result in real time. 
	Otherwise Csound would write a file as final result of its rendering.
2. The `<CsInstruments>` tag. Here all instruments are collected. This tag is
   the place for the Csound code. Sometimes it is called the Csound "Orchestra".
3. The `<CsScore>` tag. This we discussed in the previous section.

As you see, all three tags are embraced by another tag:

`<CsoundSynthesizer> ... </CsoundSynthesizer>`

This tag defines the boundaries for the Csound program you write. In other words: 
What you write out of these boundaries will not be considered by Csound.
	

## Try it yourself
- Change the duration of the instrument.
- Change the start time of the instrument.
- Change the frequency of the oscillator.
- Change the amplitude of the oscillator.


## Opcodes, Keywords, Tags and Terms you have learned in this tutorial
### Opcodes
- `poscil:a(Amplitude,Frequency)` oscillator at audio rate, with amplitude
  and frequency input.
- `outall(aSignal)` outputs *aSignal* to all output channels.

### Keywords
- `instr ... endin` are the keywords to begin and end an instrument definition 

### Tags
- `<CsoundSynthesizer>` ... `</CsoundSynthesizer>` starts and ends a Csound file.
- `<CsOptions>` ... `</CsOptions>` starts and ends the [Csound Options](What-are-Csound-Options.md).
- `<CsInstruments>` ... `</CsInstruments>` starts and ends the space for defining 
  Csound instruments.
- `<CsScore>` ... `</CsScore>` starts and ends the Csound score. 

### Terms
- An *audio rate* or *a-rate* signal is a signal which is updated every sample.

## Go on now ...

with the next tutorial: [02 Hello Frequency](01-GS-02.md).


## ... or read some more explanations here

### Why is a sine "the most elementary sound in the world"?

There are many answers to this question, depending on the perspective.  
For a musician, this is important to know:  
- A sine wave is the only sound which represents only one frequency.  
- All other wave forms have two or more frequencies in themselves.  
- These frequencies which are contained in one tone, are called **partials**.  
- Each of these partials are sine waves, so a complex tone can be understood as
addition of sine waves.  
See [Chapter 04A](04-a-additive-synthesis.md) for more information.


### Traditional and functional way to write Csound code

Perhaps you are surprised to see Csound code written in the way it is described 
above. In fact, the classical way to write Csound code was like this:

~~~csound
aSine poscil 0.2, 400
      outall aSine
~~~

You are welcome to continue writing code in this way. The reasons why we use the
"functional" way of writing Csound code are:

1. Although the traditional way of writing Csound code as  
`aSine poscil 0.2, 400`  
is shorter than  
`aSine = poscil:a(0.2,400)`  
because we have no `=` sign, no `:a`, and no parentheses, it is less clear what
the meaning of the words "aSine" and "poscil" are. But we all knwo from school:    
`y = x + 2`  
Which means that the left side is the result of the right side.  
These conventions are important in learning a programming language, and all what
makes it a bit easier to understand, should be used.

2. As mentioned above, most other programming languages use a similar syntax,
in the form *output = function(arguments)*. So for people who already know any
other programming language, it helps learning Csound.


### About these tutorials

This *Getting Started* has been written by Joachim Heintz in 2023. It is based
on many experiences in teaching Csound. Thanks goes to all students and friends,
from Hanover University of Music, from Yarava Music Group Tehran, and elsewhere,
who gave me feedback and contributed in one or another way to this and other
texts and contents. Amin and Parham, Marijana and Betty, Tom and Farhad, Ehsan 
and Vincent, Julio and Arsalan, to name some of them.

Other thanks go to ...

This *Getting Started* is supposed to be one amongst many. Its focus is on
learning the language: how to think and to program in Csound. I believe that
who understands it, and enjoys Csound's simplicity and clarity, has all doors
open to go anywhere, and learn about exquisit sounds and the infinite possibilities
in Csound to create and shape them. Iain McCurdy's examples, on his 
[website](http://iainmccurdy.org/csound.html)
or inside [Cabbage](https://cabbageaudio.com/)
are an inexhaustible source, amongst others.
