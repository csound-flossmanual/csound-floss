# 01 Hello Csound

## What you learn in this tutorial
- How to create and output a sine tone.
- What Csound opcodes are.
- What is audio rate in Csound.
- How a Csound .csd document is structured.

## What is a Sine Oscillator
A sine wave can be seen as the most elementary sound in the world.
When we look at a sine wave in an audio editor, it looks like this:

![Sine wave](../resources/images/01-a-sinewave.png){width=95%}

To produce a sine wave, Csound uses an oscillator.
An oscillator needs two informations as input:  
1. Which is the maximum amplitude to output.  
2. How many periods (cycles) per second to perform.

## A Sine Oscillator in Csound
Csound has many different oscillators.
(See [here]() for some description and comparisions.)  
We use here the opcode `poscil` which means "precise oscillator".  
An **opcode** is a processing unit in Csound, like an "object" in PureData or Max,
or a "UGen" in SuperCollider.  
The inputs of an opcode are called **arguments** and are written in parentheses
immediately after the opcode name: `poscil(0.2,400)`.  
This is very common in programming languages, like `range(13)` in Python,
or `printf("no no")` in C, or `Date.now()` in Javascript (in this case with 
empty parentheses which means: no input arguments).

## A Signal Flow and its Code
We create now a sine wave of 0.2 amplitude and 400 cycles per second (Hz).  
We call this signal *aSine* because it is an audio signal.  
An audio signal is a signal which produces a new value every sample.
(Learn more [here]() about sample rate.)  
This is the line in Csound to produce this signal:

    aSine = poscil:a(0.2,400)
	
This means: The signal *aSine* is created by the opcode `poscil` at audio rate
(`:a`), and the input for `poscil` is 0.2 for the amplitude and 400 for the 
frequency.

To output the signal (so that we can hear it), we put it in the `outall` 
opcode:

    outall(aSine)
	
Note that the signal *aSine* at first was the output of the oscillator, and 
then became input of the `outall` opcode. This is a typical chain which is 
well knows from modular synthesizers: A cable connects the output of one 
module with the input of another.

We can draw the program flow like this:

... IMAGE

## Your first Csound instrument
In Csound, all oscillators, filters, sample players, so anything what "does 
something", is encapsulated in an **instrument**. An instrument has the keyword 
`instr` at its start, and `endin` at its end. 

After `instr`, seperated by a space, we assign a number (1, 2, 3, ...) or a 
name to the instrument. Let us call our instrument "Hello", and include the 
code which we developed:


    instr Hello
      aSine = poscil:a(0.2,400)
      outall(aSine)
    endin


## Example

We are now ready to run the code. All we have to do is, to put the 
instrument in a complete Csound file.

Look at the code in the example. Can you retrieve the instrument code?

Push the "Play" button. You should hear two seconds of a 400 Hz sine tone.

Can you see why it plays for two seconds?

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  aSine = poscil:a(0.2,400)
  outall(aSine)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~

## The Csound Score

At the bottom of the example code, you find this:


    <CsScore>
    i "Hello" 0 2
    </CsScore>
    

This is the **Score** section of the .csd File.
it starts with the tag `<CsScore>` and ends with `</CsScore>`.
In between there is this score line:

    i "Hello" 0 2
	
The `i` means: This is an instrument event.  
The `"Hello"` means: It is about the "Hello" instrument.  
The `0` means: Start time of this instrument is 0 (= start immediately).  
The `2` means: The duration of this instrument is 2 (seconds).


## The Csound Document Structure

The whole Csound document consists of three parts:

1. The `<CsOptions>` tag. You see here the statement:
	`-o dac`
	This means: The output (`-o`) shall be written to the digital-to-analog converter
	(`dac`). Because of this, we listen to the result in real time. 
	Otherwise Csound would write a file as final result of its rendering.
2. The `<CsInstruments>` tag. Here all instruments are collected. This tag is
   the place for the Csound code. Sometimes it is called the Csound "Orchestra".
3. The `<CsScore>` tag. This we discussed.

As you see, all three tags are embraced by another tag:

`<CsoundSynthesizer> ... </CsoundSynthesizer>`

This tag defines the boundaries for the Csound program you write. In other words: 
What you write out of these boundaries will not be considered by Csound.
	

## Try it yourself
- Change the duration of the instrument.
- Change the start time of the instrument.
- Change the frequency of the oscillator.
- Change the amplitude of the oscillator.


## Opcodes, Keywords and Tags you have learned in this tutorial
### Opcodes
- `poscil:a(iAmplitude,iFrequency)` oscillator at audio rate
- `outall(aSignal)` outputs *aSignal* to all output channels

### Keywords
- `instr ... endin` are the keywords to begin and end an instrument definition 

### Tags
- `<CsoundSynthesizer>` ... `</CsoundSynthesizer>` starts and ends a Csound file.
- `<CsOptions>` ... `</CsOptions>` starts and ends the [Csound Options](What-are-Csound-Options.md).
- `<CsInstruments>` ... `</CsInstruments>` starts and ends the space for defining 
  Csound instruments.
- `<CsScore>` ... `</CsScore>` starts and ends the Csound score. 


## Go on now ...

with the next tutorial: [02 Hello Frequency](01-GS-02.md).


## ... or read some more explanations here

### Why is a sine "the most elementary sound in the world"?

There are many answers to this question, depending on the perspective.  
For a musician, this is important to know:  
- A sine wave is the only sound which represents only one frequency.  
- All other wave forms have two or more frequencies in themselves.  
- These frequencies which are contained in one tone, are called **partials**.  
- Each of these partials are sine waves, so a complex tone can be understood as
addition of sine waves.  
See [Chapter 04A](04-a-additive-synthesis.md) for more information.


### Traditional and functional way to write Csound code

