# 01 Hello Csound

### What you learn in this tutorial

- How to create and output a **sine** tone.
- What Csound **opcodes** are.
- What **audio rate** in Csound means, and
- What an **audio variable** is.
- How we can draw a **signal flow**.
- What a Csound **instrument** is, and
- What a Csound **score** is.

## What is a Sine Oscillator

A sine wave can be seen as the most elementary sound in the world.
When we draw a sine wave as a graph showing amplitude over time,
it looks like this:

![Sine wave snake](../resources/images/01-GS-01-sine.png)

To produce a sine wave, Csound uses an oscillator.
An oscillator needs certain input in order to run:

1. A maximum amplitude to output. This results in louder or softer tones.

2. The number of periods (cycles) per second to create. This results in higher or
   lower pitches.
   The unit is _Hertz (Hz)_. 1000 Hz would mean that a sine
   has 1000 periods in each second.

## A Sine Oscillator in Csound: Opcode and Arguments

Csound has many different oscillators.
(You can find [here](02-opcodes.md) some descriptions and comparisions.)
In this example, we use the opcode `poscil` which means "precise oscillator".

An **opcode** is a processing unit in Csound, like an "object" in PureData or Max,
or a "UGen" in SuperCollider.
If you are familiar with programming languages, you can see an opcode as a
built-in function.

The inputs of an opcode are called **arguments** and are written in parentheses
immediately after the opcode name.
So `poscil(0.2,400)` means: The opcode `poscil` gets two input arguments.

- The first argument is the number `0.2`.
- The second argument is the number `400`.

The meaning of the input arguments depends on how the opcode is implemented.
For `poscil`, the first input is the amplitude, and the second input is the
frequency.
The [Csound Reference Manual](https://csound.com/docs/manual/index.html) contains
the information about it.
We will give in [Tutorial 08](01-GS-08.md) some help how to use it.

This way of writing code is very common in programming languages, like `range(13)`
in [Python](<https://en.wikipedia.org/wiki/Python_(programming_language)>),
or `printf("no no")` in
[C](<https://en.wikipedia.org/wiki/C_(programming_language)>), or `Date.now()` in
[JavaScript](https://en.wikipedia.org/wiki/JavaScript) (in the latter case with
empty parentheses which means: no input arguments).

Note: There is also another way of writing Csound code.
See [below](#traditional-and-functional-way-to-write-csound-code) if
you want to learn more about it.

## A Signal Flow and its Code

We now create a sine wave of 0.2 amplitude and 400 cycles per second (Hz).

We will call this signal _aSine_ because it is an audio signal.
The character **a** at the beginning of the variable name
signifies exactly this.

An audio signal is a signal which produces a new value every sample.
(Learn more [here](15-a-digital-audio.md) about samples and sample rate).

This is the code line in Csound to produce the _aSine_ signal:

    aSine = poscil:a(0.2,400)

This means: The signal _aSine_ is created by the opcode `poscil` at audio rate
(`:a`), and the input for `poscil` is 0.2 for the amplitude and 400 for the
frequency.

To output the signal (so that we can hear it), we put it in the `outall`
opcode. This opcode sends an audio signal to all available output channels.

    outall(aSine)

Note that the signal _aSine_ at first was the output of the oscillator, and
then became input of the `outall` opcode. This is a typical chain which is
well known from modular synthesizers: A cable connects the output of one
module with the input of another.

We can draw the program flow like this:

![Signal flow and Csound code for sine oscillator and output](../resources/images/01-GS-01-a.png){width=80%}

In the middle you see the signal flow, with symbols for the oscillator and
the output. You can imagine them as modules of a synthesizer, connected by a
cable called _aSine_.

On the left hand side you see the chain between input, opcode and output. Note that
the output of the first chain, contained in the _aSine_ variable, becomes the
input of the second chain.

On the right hand side you see the Csound code. Each line of code represents one
_input -> opcode -> output_ chain, in the form _output = opcode(input)_.
The line `outall(aSine)` does not have an output in Csound, because it sends
audio to the hardware (similar to the "dac~" object in PD or Max).

## Your first Csound instrument

In Csound, all oscillators, filters, sample players and other processing units
are encapsulated in an **instrument**. An instrument has the
keyword `instr` at its start, and `endin` at its end.

After the keyword `instr`, seperated by a space, we assign a number (1, 2, 3, ...) or a
name to the instrument. Let us call our instrument "Hello", and include the
code which we discussed:

    instr Hello
      aSine = poscil:a(0.2,400)
      outall(aSine)
    endin

## Example

We are now ready to run the code. All we have to do is put the
instrument in a complete Csound file.

Look at the code in the example. Can you find the instrument code?

Push the "Play" button. You should hear two seconds of a 400 Hz sine tone.

Can you see why it plays for two seconds?

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  aSine = poscil:a(0.2,400)
  outall(aSine)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
```

## The Csound Score

At the bottom of the example code, you find this:

    <CsScore>
    i "Hello" 0 2
    </CsScore>

This is the **Score** section of the .csd File.
it starts with the tag `<CsScore>` and ends
with `</CsScore>`.
Between these two tags is this score line:

    i "Hello" 0 2

Each column (parameter field) specifies certain information:  
`i`: This is an instrument event.  
`"Hello"`: The instrument which this score line refers to.  
`0`: The start time of this instrument, 0 (= start immediately).  
`2`: The duration of this instrument, 2 (seconds).

## Try it yourself

(You can edit the example just by typing in it.)

- Change the duration of the instrument.
- Change the start time of the instrument.
- Change the frequency of the oscillator.
- Change the amplitude of the oscillator.

## Opcodes, Keywords and Terms you have learned in this tutorial

### Opcodes

- `poscil:a(Amplitude,Frequency)` oscillator at audio rate, with amplitude
  and frequency input.
- `outall(aSignal)` outputs _aSignal_ to all output channels.

### Keywords

- `instr ... endin` are the keywords to begin and end an instrument definition.

### Terms

- An _audio rate_ or _a-rate_ signal is a signal which is updated sample by sample.

## Go on now ...

with the next tutorial: [02 Hello Frequency](01-GS-02.md).

## ... or read some more explanations here

### Why is a sine "the most elementary sound in the world"?

To be honest, I like sine tones. I know that many people find them boring.
I like their simplicity, so against all good advices I will spend the first ten
Tutorials with sine tones only. Sorry for being my victim to it ...

But back to the question: What is elementary at sine waves?

From a mathematical point of view it is quite fascinating that we can
understand and construct a sine as constant movement of a point on a circle.
This is called [simple harmonic motion](https://en.wikipedia.org/wiki/Simple_harmonic_motion)
and is fundamental for many phenomena in the physical world, including sound.

For musical acoustics, there is another meaning of sines being elementary.

A sine tone is the only sound which represents only **one** pitch.
All other sounds have two or more pitches in themselves.

This means: All other sounds can be understood as addition of simple sine
tones. These sines which are inside a periodic sound, like a sung tone or other
natural pitched sounds, are called **partials** or **harmonics**.

Although the sounding reality is a bit more complex, it shows that sine waves
can somehow be understood as most elementary sounds, at least in the world
of pitches.

You can find more about this subject in
the [Additive Synthesis](04-a-additive-synthesis.md) chapter and in
the [Spectral Resynthesis](/sound-modification/fourier-analysis-spectral-processing)
chapter of this book.

### Traditional and functional way to write Csound code

Perhaps you are surprised to see Csound code written in the way it is described
above. In fact, the classical way to write Csound code is like this:

```csound
aSine poscil 0.2, 400
      outall aSine
```

You are welcome to continue writing code in this way. The reasons why I use the
"functional" way of writing Csound code in these Tutorials are:

1. We are all familiar with this way to declare a left hand side variable `y`
   to be the sum of another variable `x` plus two:
   `y = x + 2`
   Or, to yield a `y` as function of `x`:
   `y = f(x)`
   I think it is good to build upon this familiarity. It is hard enough for a
   musician to learn a programming language, never having heard about variables,
   signal flow, input arguments, or parameters. What ever let you feel more
   familiar in this new world is helpful and should be used.

2. As mentioned above, most other programming languages use a similar syntax,
   in the form _output = function(arguments)_. So for people who already know any
   other programming language, it helps learning Csound.

3. The functional style of writing Csound code has always been there in
   expressions like `ampdb(-10)` or `ftlen(giTable)`. So it is somehow not
   completely new but an extension.

4. Whenever we want to use an expression as argument (you will learn more about
   it in [Tutorial 06](01-GS-06.md)) we need this way of writing code. So it is good
   to use it as consistent as possible.

### About these tutorials

This _Getting Started_ has been written by Joachim Heintz in 2023. It is based
on many experiences in teaching Csound to young composers.
Thanks goes to all students and friends,
from Hanover University of Music, from Yarava Music Group Tehran, and elsewhere,
who gave me feedback and contributed in one or another way to this and other
texts and contents. Amin and Parham, Marijana and Betty, Tom and Farhad, Ehsan
and Vincent, Julio and Arsalan, to name some of them.

Thanks go also to Wolfgang Fohl, Tarmo Johannes, Rory Walsh and others for their comments and help.

And of course to the Core Developers whose great work made it possible that we
all can use Csound in the way we do.
I hope this Tutorial can show to some more musicians how
admirable and successful the big effort was and is to turn the oldest audio
programming language into a modern one, without losing any composition written in the Csound language decades ago.

Each Tutorial has a first part as _must read_ (well which _must_ is that),
followed by an optional part
(in which of course the most interesting things reside). To make at least one
thing in the world reliable, each _must read_ consists of five headings, and
each _can read_ of three. Actually I planned it to be 4+3, but then I asked Csound,
and received this answer:

    if 4+3 == 7 then
      "Write this Tutorial!\n"
    else
      8!
    endif

So I took this as oracle and decided for 5+3 headings, to fulfill also
the `8!` requirement. It is always better to satisfy both gods of a conditional
branch, in my experience.

Included in the _must read_ part is a _must do_. At first a central example,
yes very central, substantial, expedient, enjoyable, and of course very
instructive. And then a _Try it yourself_ which is kind of the dark side of
the example: As easy it is to just push the "Run" button, as hard will it
perhaps be to solve these damned exercises. But, to quote John ffitch:
"We have all been there ..."

I guess, no I have run a long series of tests, no it has been proven by
serious studies held by the most notable and prestigious universities
in the most important parts of the world, that the average time needed to
go through one Tutorial, is one hour. So once I finished the planned number
of 24 Tutorials then finally the goal is reached to **learn Csound in one day**.

Nevertheless, I must admit that this _Getting Started_ can only be one amongst many.
Its focus is on
learning the language: How to think and to program in Csound. I believe that for
those who understand this, and enjoy Csound's simplicity and clarity, all doors are
open to go anywhere in the endless world of this audio programming language.
It be live coding, Bela Board or Raspberry Pi, it be noise music or the most
soft and subtle sounds, it be fixed media or the fastest possible real-time
application, it be using Csound as standalone or as a plugin to your preferred DAW.
As for sounds, please have a look, and in particular an ear, to
Iain McCurdy's examples, either on his [website](http://iainmccurdy.org/csound.html) or
inside [Cabbage](https://cabbageaudio.com/).
They are an inexhaustible source of inspiration and a yardstick for sound quality.
