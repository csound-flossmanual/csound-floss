# 04 Hello Fade-out

### What you learn in this tutorial

- How to apply **linear fades** to an instrument.
- What **p-fields** are.
- What **p3** in an instrument means.

## Fade-in and Fade-out

Currently we have a rude end of the tone which is created by our instrument.
This is due to a brutal cutoff at the end of the instrument duration.

Imagine a sine wave which is cut anywhere. Perhaps this will happen:

![Click at the end of a sine wave](../resources/images/01-GS-04-a.png)

In this case we will hear a "click" at the end.

But even without this click: Usually we will prefer a soft end, similar to
how natural tones end.

The Csound opcode `linen` is very useful for applying simple fades to an audio signal.

## The 'linen' Opcode

The envelope which is created by `linen` looks like this:

![The linen opcode](../resources/images/01-GS-04-b.png)

We need three numbers to adjust the fades in `linen`:
(1) The time to fade in.
(2) The overall duration.
(3) The time to fade out.

## The Score Parameter Fields

Usually we want to set `linen`'s overall duration to the note's duration.

For instance, if the note's duration is 2 seconds, we will want 2 seconds
for `linen`'s overall duration.
If the note's duration is 3 seconds, we will want 3 seconds
for `linen`'s overall duration.

In [Tutorial 01](/get-started/GS-01#the-csound-score), you have learned how
the instrument duration is set in Csound.

You will remember that this is written in the score:

    i "Hello" 0 2

After the starting `i` which indicates an "instrument event", we have three
values:

1. The name of the instrument
2. The start time
3. The duration

We call these values **parameter fields**, or **p-fields**.

We have three p-fields in our score, abbreviated as **p1**, **p2** and **p3**.

![Score parameter fields](../resources/images/01-GS-04-c.png)

## 'p3' in an Instrument

A Csound instrument is instatiated via a score line.

Each score line has at least three parameter fields.

These parameters are passed to the instrument when it is instantiated.

So each instrument "knows" its **p1** (its number or name), its **p2** (its
start time), and its **p3** (its duration).

And more:

Each instrument can **refer** to its score parameter fields by just
writing **p1** or **p2** or **p3** in the code.

## Example

In addition to the three input arguments for `linen` which we have discussed
above, we have as first input argument the audio signal which we want to modify.

This is the order of the four input arguments for the `linen` opcode:

1. The audio signal to which the fade-in and fade-out is applied.
2. The fade-in duration (in seconds).
3. The overall duration for `linen`.
4. The fade-out duration.

Look at the code. Can you see how the code uses **p3** to adapt the duration
of `linen` to the duration of the instrument?

```csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kAmp = linseg:k(0.3,0.5,0.1)
  kFreq = linseg:k(500,0.5,400)
  aSine = poscil:a(kAmp,kFreq)
  aOut = linen:a(aSine,0,p3,1)
  outall(aOut)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
```

## Volume Change as Multiplication

How can we apply the signal which we have plotted above, moving as
fade-in from 0 to 1, and as fade-out from 1 to 0, to an audio signal?

This is done by multiplication.

We multiply the audio signal with the envelpe signal which `linen` generates.

    aEnv = linen:a(1,0.5,p3,0.5)
    aSine = poscil:a(0.2,400)
    aOut = aEnv * aSine

Actually, the form

    aOut = linen:a(aSine,0.5,p3,0.5)

is a shortcut for this multiplication.

## Try it yourself

Change the code so that

- You add a fade-in of 0.5 seconds. (Currently we only have a fade-out.)
- You make the instrument play for 5 seconds, instead of 2 seconds.
- You apply a fade-out for the whole duration of the instrument, regardless
  what this duration be.
- You apply a fade-in of 1 second, but no fade-out.
- You apply a fade-in of half the instrument's duration, and a fade-out of
  also half the instrument's duration. (This will result in a triangular
  envelope shape.)
- You insert **p3** in the _kAmp_ line so that the amplitude changes over
  the whole duration of the instrument.
- You insert **p3** in the _kFreq_ line so that the frequency changes over
  the whole duration of the instrument.

## Opcodes and symbols you have learned in this tutorial

### Opcodes

- `linen:a(aIn,Fade-in,Duration,Fade-out)` linear fade-in and fade-out

### Symbols

- `p1` the first parameter of the score line which calls the instrument:
  number or name of the instrument
- `p2` the second parameter of the score line which calls the instrument:
  the start time of the instrument
- `p3` the third parameter of the score line which calls the instrument:
  the duration of the instrument

Note: `p3` refers to the score, but inside the score it has no meaning,
and Csound will throw an error if you write **p3** as symbol in the score.
It only has a meaning in the **instrument** code.

## Go on now ...

with the next tutorial: [05 Hello MIDI Keys](01-GS-05.md).

## ... or read some more explanations here

### 'linen's amplitude input

The first input for `linen` is the amplitude.

In the most simple case this amplitude is a fixed number.
If it is 1, then we have the basic shape which we saw at the beginning of
this chapter:

![linen with amplitude 1](../resources/images/01-GS-04-d.png)

`linen:a(1,0.2,p3,0.5)` will perform a fade-in from 0 to 1 in 0.2 seconds,
and a fade-out from 1 to 0 in 0.5 seconds, over the whole
duration **p3** of the instrument event.

In the example, we have used the `linen` opcode in directly inserting an audio
signal in its first input.

    linen:a(aSine,0,p3,1)

As the first input of `linen` is an amplitude, it can be a constant
amplitude, or a signal. This is the case here for _aSine_ as amplitude input.

As you already know, in a signal the values change over time,
either at _k-rate_ or at _a-rate_.

### Should we not use a k-rate opcode for fades?

Perhaps you wonder why we created an **audio** signal for the envelope signal.
Would it not be more efficient to use a **control** signal for the envelope?
The code would then read:

    kFade = linen:k(1,0.5,p3,0.5)
    aSine = poscil:a(0.2,400)
    aOut = kFade * aSine

Indeed this might be slightly more efficient. But for modern computers, this is a
neglectable performance gain.

On the other hand, _a-rate_ envelopes are preferable because they are really
smooth. They have no "staircases" as _k-rate_ envelope have.

You can read more
[here](/csound-language/initialization-and-performance-pass#possible-problems-with-k-rate-tick-size)
about this subject. I personally recommend to always use a-rate envelopes.

### Is it good to have linear fades?

A linear fade is a fade which draws a line for the transition between 0 an 1,
or the transition between 1 and 0.
This is where the name "linen" comes from: a linear envelope generator.

An alternative is to use curves for fades.

![Linear versus curved fades](../resources/images/01-GS-04-e.png)

Acoustically linear fades are not the best. We will discuss the reasons
in [Tutorial 06](01-GS-06.md).

Practically, the linear fades which `linen` generates are sufficient for
many cases. But keep in mind that there are other possible shapes for fades,
and move to them when you are not satisfied how it sounds. The `transeg` opcode
will be your friend then.
