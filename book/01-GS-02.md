# 02 Hello Frequency


## What you learn in this tutorial
- How to create a moving frequency.
- What k-rate and k-variables in Csound are.


## A line for the frequency

In natural sounds, the frequency is rarely fixed.
Usually, for instance when we speak, it moves all the time, in a certain range.
The most simple case of such a movement is a line.
We need three values to construct a line:  
1. A value to start with.  
2. A time to move from this value to the target value.  
3. The target value.

This is a line which moves from 500 to 400 in 0.5 seconds, and then stays at
400 for 1.5 seconds:

![Frequency line](../resources/images/01-GS-02-a.png)

Note: Acoustically this way to move pitches is questionable.
We will discuss this in Tutorial 04.


## A line drawn with the linseg opcode
In Csound, we create a line like this with the opcode `linseg`.
This means "linear segments".
Here we only need one segment which moves from 500 to 400 in 0.5 seconds.

This is the Csound code for this line:

    kFreq = linseg:k(500,0.5,400)

You will recognize the structure `opcode(arguments)` which we already saw in
the first tutorial. Here, the opcode is `linseg`, and the arguments are 500
as first value, 0.5 as duration to move to the next value, and 400 as target
value.

But why is the variable on the left side called *__k__Freq*, and why is 
`linseg` written as `linseg:k`?


## A k-rate signal

A signal is something which changes in time.  
In digital audio, we usually have two different time resolutions.  
The finest time resolution which we can have, is the sample rate.  
It tells us how many audio samples we have in one second.  
We saw in the first tutorial a signal which moves in **audio rate**; calculating
a new value for every sample.

The second possible time resolution in Csound is less fine grained.  
It does not calculate a new value for every sample, but only for a group of
samples.  
This time resolution is called **control rate**.  

Variables in Csound which have __k__ as first character, are using the control
rate.  
After the opcode we put `:k` to make clear that this opcode uses the control
rate as its time resolution.

Now you understood why 

    kFreq = linseg:k(500,0.5,400)

has the lower case __k__ in two positions.

We will explain more about k-rate in the next tutorial.   


## Example

Push the "Play" button. You will hear a tone with a falling pitch.

Can you see how the moving line is fed into the oscillator?

~~~csound
<CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 64
nchnls = 2
0dbfs = 1

instr Hello
  kFreq = linseg:k(500,0.5,400)
  aSine = poscil:a(0.2,kFreq)
  outall(aSine)
endin

</CsInstruments>
<CsScore>
i "Hello" 0 2
</CsScore>
</CsoundSynthesizer>
~~~


## Signal flow

When you look at our instrument code, you see that there is a common scheme:

![Input output insertions](../resources/images/01-GS-02-b.png)

The first line produces a signal and stores it in the variable *kLine*.  
This variable is then used as input in the second line.

The second line produces a signal and stores it in the variable *aSine*.  
This variable is then used as input in the third line.


## Try it yourself

- Let the frequency line move upwards instead of downwards.  
- Use `linseg` to create a constant frequency of 400 or 500 Hz.  
- Make the time of the ramp or "glissando" longer or shorter.  
- Add another segment by appending one more value for time, and one more
value for the second target value.  
- Replace `linseg` by `line` and hear what is different.


## Opcodes and Terms you have learned in this tutorial

### Opcodes
- `linseg:k(Value1,Duration1,Value2,...)` generates segments of lines

### Terms
- A *control rate* or *k-rate* signal is a signal which is not updated every 
sample, but for a group or block of samples.


## Go on now ...

with the next tutorial: [03 Hello Amplitude](01-GS-03.md).


## ... or read some more explanations here

### Linseg versus Line

Csound has a `line` opcode which we could use instead of `linseg`.

We can replace `linseg` in our code with `line`:

~~~csound
instr Hello
  kFreq = line:k(500,0.5,400)
  aSine = poscil:a(0.2,kFreq)
  outall(aSine)
endin
~~~

When you run this code, you will hear that `line` has one important 
difference to `linseg`: It will not stop at the target value, but continue
its movement, in the same way as before:

![line vs linseg](../resources/images/01-GS-02-c.png)

Usually we do not want this, so it is recommended to always use `linseg`,
except some special cases.


### Coding conventions

When you press the "Run" button, Csound "reads" the code you have written.
Perhaps you already experienced that you wrote something which results in an
error, because it is "illegal".

For instance this code:

    kFreq = linseg:k(500 0.5 400)

What is illegal here? We have seperated the three input arguments for
`linseg` not by commas, but by spaces. Csound expects commas, and if there is
no comma, it returns a syntax error, and the code cannot be compiled:

    error: syntax error, unexpected NUMBER_TOKEN, 
    expecting ',' or ')' (token "0.5")

This is not a convention, it is a syntax we have to accept when we want our code
to be compiled and executed by Csound.

But inside this syntax we have many ways to write code in one was or another.

Let us look to some examples:

~~~csound
(1) kFreq = linseg:k(500,0.5,400)
(2) kFreq=linseg:k(500,0.5,400)
(3) kFreq = linseg:k(500, 0.5, 400)
(4) kFreq = linseg:k(500,.5,400)
(5) kFreq 		=		linseg:k(500,   0.5,   400)
~~~

\(1) This is the way I write code here in these tutorials. I put a space left and 
right the `=`, but I put no space after the comma.

\(2) This is possible but I think you will agree that it is less understandable
because the `=` sign is somehow hidden.

\(3) This is as widely used as (1). I remember when I first read Guido van Rossum's
Python tutorial in which we recommends to write as in (1), I did not like it at
all. It took twenty years to agree ...

\(4) This is a common abbreviation which is possible in Csound and some other
programming languages. Rather than `0.5` you can just write `.5`. I use it 
privately, but will not use it here in these tutorials because it is less clear.

\(5) You are allowed to use tabs instead of spaces, and each combination of it,
and as many tabs or spaces as you like. But usually we do not want a line to be
longer as necessary.

Another convention is to write the keywords `instr`and `endin` at the beginning
of the line, and then the code indented by two spaces:  

~~~csound
instr Hello
  kFreq = linseg:k(500,0.5,400)
  aSine = poscil:a(0.2,kFreq)
  outall(aSine)
endin
~~~

The reason for this convention is again to format the code in favor of
maximum clarity. In the Csound Book we used one space, but I think two spaces 
are a bit better.

To summarize: You have a lot of different options to write Csound code. You 
can do what you like, but it is wise to accept some conventions which serve
for maximal comprehension. The goal is readable and transparent code.


### When to choose a-rate or k-rate?

The main reason for introducing k-rate was to save CPU power. This was essential 
in the early years of computer music. Nowadays it is not the same. For usual 
instruments in electronic music, we can omit k-rate variables, and only use
a-rate variables. In our case, we can write:

    aFreq = linseg:a(500,0.5,400)
    aSine = poscil:a(0.2,aFreq)
    outall(aSine)

As a simple advice:

- Always use a-rate when it sounds better.
- Use k-rate instead of a-rate when you must save CPU power.
- Some opcodes only accept k-rate input, but no a-rate input. In this case
you must use k-rate variables.
